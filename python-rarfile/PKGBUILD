# http://aur.archlinux.org/packages/python-rarfile
pkgname=python-rarfile
_name=${pkgname#python-}
pkgver=3.3
pkgrel=1
pkgdesc="Python module for RAR archive reading"
arch=('any')
url="https://github.com/markokr/rarfile"
license=('ISC')
depends=('python')
makedepends=('python-setuptools')
optdepends=('unrar: default decompression tool'
            'unarchiver: alternative decompression tool'
            'libarchive: alternative decompression tool'
            'python-cryptography: process archives with password-protected headers (choose one)'
            'python-pycryptodome: process archives with password-protected headers (chooose one)')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('1bd2cf08f1f07e5040dcb58d0e60f50448f6e87723754eef6cc4d1c1c8e719d4')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
