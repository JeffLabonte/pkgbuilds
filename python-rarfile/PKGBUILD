pkgname=python-rarfile
_name=rarfile
pkgver=3.1
#_pkgver=3_1
pkgrel=1
pkgdesc="Python module for RAR archive reading"
arch=('any')
url="https://pypi.org/project/rarfile"
license=('ISC')
depends=('python')
makedepends=('python-setuptools')
optdepends=('unrar: Decompressing compressed files'
            'libarchive: Alternative to "unrar"'
            'python-crypto: Parsing encrypted headers')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
#source=("https://github.com/markokr/rarfile/archive/${_name}_{$_pkgver}.tar.gz")
sha256sums=('dc1062176c529f417522af7da1291a35b85c8017464e665aabce048cfe2659b6')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
