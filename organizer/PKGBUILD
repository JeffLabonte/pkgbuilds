# Maintainer: Your Name <youremail@domain.com>
pkgname=organizer-git
_gitname=organizer
pkgver=v0.103.r0.ea2bb49
pkgrel=1
pkgdesc="Python Gnome app to organize your files"
arch=('i686' 'x86_64')
url="https://gitlab.gnome.org/aviwad/$_gitname"
license=('GPL3')
depends=('python' 'gtk3' 'libhandy' 'pango' 'freetype2')
makedepends=('git' 'meson' 'appstream-glib')
provides=("$_gitname")
source=("git+https://gitlab.gnome.org/aviwad/$_gitname.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/$_gitname"
	meson . build --prefix /usr
	ninja -C build
}

package() {
	cd "$srcdir/$_gitname"
	DESTDIR="$pkgdir" ninja -C build install
}
