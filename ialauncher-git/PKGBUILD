pkgname=ialauncher-git
pkgver=r45.291048f
pkgrel=1
pkgdesc="Play all of the Internet Archiveâ€™s MS-DOS games offline!"
arch=('any')
url="https://github.com/rtts/ialauncher"
license=('GPL3')
depends=('dosbox' 'webkit2gtk' 'python' 'python-jinja' 'python-gobject' 'python-natsort')
makedepends=('git' 'python-setuptools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install="${pkgname%-git}".install
source=('git+https://github.com/rtts/ialauncher')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
	install -dm755 "$pkgdir/opt/${pkgname%-git}/games"
	cp -a games/* "$pkgdir/opt/${pkgname%-git}/games/"
}
