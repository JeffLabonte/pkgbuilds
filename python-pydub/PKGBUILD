# https://aur.archlinux.org/packages/python-pydub
pkgname=python-pydub
_name=${pkgname#python-}
pkgver=0.24.1
pkgrel=2
pkgdesc="Manipulate audio with an simple and easy high level interface"
arch=('any')
url="http://pydub.com"
license=('MIT')
depends=('python-pyaudio' 'python-scipy' 'python-simpleaudio' 'ffmpeg')
makedepends=('python-setuptools')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz"
        "https://github.com/jiaaro/pydub/raw/v$pkgver/LICENSE")
sha256sums=('630c68bfff9bb27cbc5e1f02923f717c3bc5f4d73fd685fda08b6ce90f76dc69'
            'ae856536224cc7a389e968771fc5f259814bdd0da63539cf72282adbaef6897a')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"
	python setup.py check
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 "$srcdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
