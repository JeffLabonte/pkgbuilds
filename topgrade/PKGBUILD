# http://aur.archlinux.org/packages/topgrade
pkgname=topgrade
pkgver=4.7.1
pkgrel=1
pkgdesc='Invoke the upgrade proceadure of multiple package managers'
arch=('x86_64' 'aarch64' 'armv7')
url='https://github.com/r-darwish/topgrade'
license=('GPL3')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('57d05d806ff95780fc196a5d20d0830b0b9437e3cc84a3735905446e3c0a3860')

build() {
	cd "$pkgname-$pkgver"
	cargo build --release --locked --all-features
}

check() {
	cd "$pkgname-$pkgver"
	cargo test --release --locked
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
	install -Dm644 "$pkgname.8" -t "$pkgdir/usr/share/man/man8"
	install -Dm644 config.example.toml -t "$pkgdir/usr/share/doc/$pkgname"
}
