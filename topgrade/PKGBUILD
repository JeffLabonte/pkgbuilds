# http://aur.archlinux.org/packages/topgrade
pkgname=topgrade
pkgver=5.2.0
pkgrel=1
pkgdesc='Invoke the upgrade proceadure of multiple package managers'
arch=('x86_64' 'aarch64' 'armv7')
url='https://github.com/r-darwish/topgrade'
license=('GPL3')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('438e9660ed49d5cd9104be6da83e03538f4d7e2b02701fe902cc770a4b805e28')

build() {
	cd "$pkgname-$pkgver"
	cargo build --release --locked
}

check() {
	cd "$pkgname-$pkgver"
	cargo test --release --locked
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
	install -Dm644 "$pkgname.8" -t "$pkgdir/usr/share/man/man8"
	install -Dm644 config.example.toml -t "$pkgdir/usr/share/doc/$pkgname"
}
