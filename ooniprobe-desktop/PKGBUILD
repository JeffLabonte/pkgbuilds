pkgname=ooniprobe-desktop
_pkgver=3.0.0-rc.6
pkgver=3.0.0.rc.6
pkgrel=1
pkgdesc="The next generation OONI Probe desktop app"
arch=('x86_64')
url="https://ooni.org"
license=('MIT')
depends=('libnotify' 'libappindicator-gtk3' 'libxtst' 'nss' 'libxss')
makedepends=('yarn')
conflicts=("${pkgname%-desktop}")
replaces=("${pkgname%-desktop}")
source=("$pkgname-$pkgver.tar.gz::https://github.com/ooni/probe-desktop/archive/v$_pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('9d82dec783c0e9b44c3e33151a3686d711aa1ecfc69918d4c4e9cf2d107eb1ff'
            'baaf4f3cca079dddc0b4e048c8778c6cc84786bb88fd9d218424b7b9f04f1135')

prepare() {
	cd "probe-desktop-$_pkgver"

	# Disable building of rpm & tar.gz
	sed -i 's/"deb",/"deb"/g' package.json
	sed -i '41,42d' package.json
}

build() {
	cd "probe-desktop-$_pkgver"
	yarn install --cache-folder "$srcdir/yarn-cache"
	yarn run probe-cli
	yarn run pack:linux
}

package() {
	cd "probe-desktop-$_pkgver"
	install -dm755 "$pkgdir/opt/OONI Probe"
	cp -a dist/linux-unpacked/* "$pkgdir/opt/OONI Probe"

	install -dm755 "$pkgdir/usr/bin"
	ln -sf "/opt/OONI Probe/$pkgname" "$pkgdir/usr/bin/$pkgname"

	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"

	for icon_size in 16 48; do
		icons_dir=/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
		install -d $pkgdir/$icons_dir
		install -m644 dist/.icon-set/icon_${icon_size}x${icon_size}.png \
			$pkgdir$icons_dir/$pkgname.png
	done

	for icon_size in 32 64 128 256 512 1024; do
		icons_dir=/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
		install -d $pkgdir/$icons_dir
		install -m644 dist/.icon-set/app_${icon_size}.png \
			$pkgdir$icons_dir/$pkgname.png
	done
}
