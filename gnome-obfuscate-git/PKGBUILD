# http://aur.archlinux.org/packages/gnome-obfuscate-git
pkgname=gnome-obfuscate-git
pkgver=0.0.2.r16.gadab581
pkgrel=1
pkgdesc="Censor private information."
arch=('x86_64')
url="https://gitlab.gnome.org/World/obfuscate"
license=('GPL')
depends=('gtk3')
makedepends=('git' 'meson' 'rust')
checkdepends=('appstream')
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
source=('git+https://gitlab.gnome.org/World/obfuscate.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/obfuscate"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson obfuscate build
	meson compile -C build
}

check() {
	meson test -C build

	cd "$srcdir/obfuscate"
	cargo test --release
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
