# http://aur.archlinux.org/packages/mangohud
pkgname=('mangohud' 'lib32-mangohud' 'mangohud-common')
pkgbase=mangohud
pkgver=0.5.1
pkgrel=3
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('meson' 'python-mako' 'glslang' 'libglvnd' 'lib32-libglvnd'
             'libxnvctrl' 'vulkan-headers')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('3e91d4fc7369d46763894c13f3315133871dd02705072981770c3cf58e8081c6')

build() {
	arch-meson \
		-Duse_system_vulkan=enabled \
		-Dld_libdir_prefix=true \
		-Dinclude_doc=false \
		"MangoHud-$pkgver" build64
	meson compile -C build64

	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	export LLVM_CONFIG="/usr/bin/llvm-config32"

	arch-meson \
		--libdir lib32 \
		-Duse_system_vulkan=enabled \
		-Dld_libdir_prefix=true \
		-Dinclude_doc=false \
		"MangoHud-$pkgver" build32
	meson compile -C build32
}

package_mangohud() {
	depends=('mangohud-common' 'gcc-libs' 'libx11' 'vulkan-icd-loader')
	optdepends=('libxnvctrl: Support for older NVIDIA GPUs')

	DESTDIR="$pkgdir" meson install -C build64

	rm -rf "$pkgdir/usr/share/man"
}

package_lib32-mangohud() {
	depends=('mangohud-common' 'lib32-gcc-libs' 'lib32-libx11'
	         'lib32-vulkan-icd-loader')
	optdepends=('lib32-libxnvctrl: Support for older NVIDIA GPUs')

	DESTDIR="$pkgdir" meson install -C build32

	rm -rf "$pkgdir"/usr/{bin,share/man}
	mv "$pkgdir/usr/share/vulkan/implicit_layer.d/MangoHud.json" \
		"$pkgdir/usr/share/vulkan/implicit_layer.d/MangoHud.x86.json"
}

package_mangohud-common() {
	pkgdesc="Common files for MangoHud"

	DESTDIR="$pkgdir" meson install -C build64

	rm -rf "$pkgdir"/usr/{bin,lib,share/vulkan}

	cd "MangoHud-$pkgver"
	install -Dm664 bin/MangoHud.conf \
		"$pkgdir/usr/share/doc/$pkgbase/MangoHud.conf.example"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgbase"
}
