pkgname=python-pynput
_name=${pkgname#python-}
pkgver=1.6.5
pkgrel=1
pkgdesc="Python library to monitor and control user input devices"
arch=('any')
url="https://github.com/moses-palmer/pynput"
license=('LGPL3')
depends=('python-xlib' 'python-six')
makedepends=('python-setuptools' 'python-wheel')
optdepends=('python-sphinx: build online documentation')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz"
        'python-pynput.patch')
sha256sums=('b8a6efac6e50ca86d0354da2070a07826eec46a1e0c02f47ef09523d380a734d'
            'b2e10f5baf1501e8365b1a95fd756cde5a8510107d89557ab764a47c3ef47226')

prepare() {
	cd "$_name-$pkgver"
	patch -Np1 -i "$srcdir/python-pynput.patch"
}

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
