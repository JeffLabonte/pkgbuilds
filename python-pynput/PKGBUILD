pkgname=python-pynput
_name=${pkgname#python-}
pkgver=1.6.8
pkgrel=1
pkgdesc="Python library to monitor and control user input devices"
arch=('any')
url="https://github.com/moses-palmer/pynput"
license=('LGPL3')
depends=('python-xlib' 'python-six')
makedepends=('python-setuptools-lint')
optdepends=('python-sphinx: build online documentation')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz"
        "$pkgname.patch")
sha256sums=('68c1863d6a1520b44b6a915e866cbfa1b8d127aef9289f25183c93e28ee5049a'
            'b2e10f5baf1501e8365b1a95fd756cde5a8510107d89557ab764a47c3ef47226')

prepare() {
	cd "$_name-$pkgver"
	patch -Np1 -i "$srcdir/python-pynput.patch"
}

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
