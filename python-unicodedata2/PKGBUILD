pkgname=python-unicodedata2
_name=${pkgname#python-}
pkgver=13.0.0
pkgrel=1
pkgdesc="Unicodedata backport updated to the latest Unicode version."
arch=('x86_64')
url="https://github.com/mikekap/unicodedata2"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
#source=("https://pypi.python.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
source=("https://github.com/mikekap/$_name/archive/$pkgver.tar.gz")
sha256sums=('149c9d637c44163b42baccd75167c31f76bfcc4809581547d10fc3550be8a814')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
