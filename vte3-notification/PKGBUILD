# https://gitlab.manjaro.org/packages/community/vte3-notification
# https://aur.archlinux.org/packages/vte3-notification
pkgname=('vte3-notification' 'vte-notification-common')
pkgbase=vte3-notification
pkgver=0.60.1
pkgrel=2
pkgdesc='Virtual Terminal Emulator widget for use with GTK3 with Fedora patches'
arch=('i686' 'x86_64')
url='https://wiki.gnome.org/Apps/Terminal/VTE'
license=('LGPL')
makedepends=('git' 'intltool' 'gobject-introspection' 'gtk-doc' 'meson' 'pango'
             'vala' 'gperf' 'glade')
options=('!emptydirs')

# Fedora patches: https://pkgs.fedoraproject.org/cgit/rpms/vte291.git/tree/
_frepourl='https://src.fedoraproject.org/rpms/vte291'
_frepobranch='f32'
_fpatchfile='vte291-cntnr-precmd-preexec-scroll.patch'
_fcommit='c2f2db14c1a858d79c69f344650c38987a675d09'

# VTE source ref
_vtetag=${pkgver}

source=(
	"git+https://git.gnome.org/browse/vte#tag=$_vtetag"
	"${_fpatchfile}-${_fcommit}::${_frepourl}/raw/${_fcommit}/f/${_fpatchfile}"
)
sha256sums=('SKIP'
            '714470357ea008de821d62f60e551351d2379a4ef723410909fba3f68e7b57b0')

prepare () {
	cd "$srcdir/vte"
	patch -p1 -i "../${_fpatchfile}-${_fcommit}"
}

build() {
	arch-meson vte build \
		-Db_lto=false \
		-D docs=true
	ninja -C build
}

check() {
	meson test -C build \
		--print-errorlogs
}

package_vte3-notification(){
	depends=('gtk3' 'pcre2' 'gnutls' 'vte-notification-common')
	provides=("vte3=$pkgver" 'libvte-2.91.so=0-64')
	conflicts=('vte3')

	DESTDIR="$pkgdir" meson install -C build

	mv "$pkgdir/etc/profile.d/vte.sh" "$srcdir"
	mv "$pkgdir/etc/profile.d/vte.csh" "$srcdir"
	mv "$pkgdir/usr/lib/vte-urlencode-cwd" "$srcdir"
}

package_vte-notification-common() {
	pkgdesc='Common files used by vte and vte3'
	arch=('any')
	provides=("vte-common=${pkgver}")
	conflicts=('vte-common')

	install -Dm644 vte.sh -t "$pkgdir/etc/profile.d"
	install -Dm644 vte.csh -t "$pkgdir/etc/profile.d"
	install -Dm755 vte-urlencode-cwd -t "$pkgdir/usr/lib"
}
