# http://aur.archlinux.org/packages/python-rebulk
pkgname=python-rebulk
_name=${pkgname#python-}
pkgver=2.0.1
pkgrel=4
pkgdesc="Define simple search patterns in bulk to perform advanced matching on any string."
arch=('any')
url="https://github.com/Toilal/rebulk"
license=('MIT')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
optdepends=('python-regex')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('320ded3cc456347d828f95e9aa5f8bab77ac01943cad024c06012069fe19690a')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"
	python setup.py test
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
