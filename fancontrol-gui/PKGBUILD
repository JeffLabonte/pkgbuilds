pkgname=fancontrol-gui
pkgver=0.8
pkgrel=1
pkgdesc="GUI for the fancontrol script and systemd service"
arch=('i686' 'x86_64')
url="https://github.com/Maldela/Fancontrol-GUI"
license=('GPL2')
depends=('qt5-quickcontrols2' 'qt5-declarative' 'lm_sensors' 'kirigami2' 'kdbusaddons'
		 'knotifications' 'kdeclarative' 'kauth' 'kpackage' 'ki18n' 'kconfig' 'systemd')
makedepends=('git' 'extra-cmake-modules' )
replaces=('fancontrol-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Maldela/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('c05b73d8aba481603f7d3958dbb7b885494c0160a4eca725189f3995cf85ab00')

build() {
	cd "$pkgname-$pkgver"
	cmake . \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLIB_INSTALL_DIR=lib \
		-DBUILD_TESTING=off \
		-DSTANDARD_CONFIG_FILE=/etc/fancontrol \
		-DSTANDARD_SERVICE_NAME=fancontrol \
		-DBUILD_GUI=on \
		-DBUILD_KCM=off \
		-DBUILD_HELPER=on \
		-DBUILD_PLASMOID=off \
		-DINSTALL_SHARED=on
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
