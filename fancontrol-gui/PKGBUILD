pkgname=fancontrol-gui
pkgver=0.7
pkgrel=1
pkgdesc="GUI for the fancontrol script and systemd service"
arch=('i686' 'x86_64')
url="https://github.com/Maldela/Fancontrol-GUI"
license=('GPL2')
depends=('qt5-quickcontrols2' 'qt5-declarative' 'lm_sensors' 'kirigami2' 'kdbusaddons' 'knotifications' 'kdeclarative' 'kauth' 'kpackage' 'ki18n' 'kconfig' 'systemd')
makedepends=('git' 'cmake' 'extra-cmake-modules' )
conflicts=('fancontrol-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Maldela/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('099a7a0c8c64709e84159dc375211b10e00b0b022f3f1733ba2c27ee1cabc751')

build() {
	cd "$pkgname-$pkgver"
	mkdir build
	cmake . \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLIB_INSTALL_DIR=lib \
		-DBUILD_TESTING=off \
		-DSTANDARD_CONFIG_FILE=/etc/fancontrol \
		-DSTANDARD_SERVICE_NAME=fancontrol \
		-DBUILD_GUI=on \
		-DBUILD_KCM=off \
		-DBUILD_HELPER=on \
		-DINSTALL_SHARED=on \
		-DKDE_INSTALL_USE_QT_SYS_PATHS=on
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
