pkgname=file-roller
pkgver=3.35.90
pkgrel=1
pkgdesc="Create and modify archives"
url="https://wiki.gnome.org/Apps/FileRoller"
arch=('x86_64')
license=('GPL')
depends=('gtk3' 'dconf' 'libarchive' 'file' 'json-glib' 'libnotify' 'zip' 'unzip')
makedepends=('yelp-tools' 'git' 'libnautilus-extension' 'meson' 'appstream-glib')
optdepends=('p7zip: 7z, arj, exe and encrypted zip files support'
            'unrar: better RAR archive support'
            'unace: ACE archive support'
            'lrzip: lrzip archive support')
groups=('gnome')
source=("git+https://gitlab.gnome.org/GNOME/file-roller.git#tag=$pkgver")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --tags | sed 's/-/+/g'
}

build() {
	cd "$srcdir/$pkgname"
	arch-meson build
	ninja -C build
}

check() {
	cd "$srcdir/$pkgname"
	meson test -C build --print-errorlogs
}

package() {
	cd "$srcdir/$pkgname"
	DESTDIR="$pkgdir" meson install -C build
}
