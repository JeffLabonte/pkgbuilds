pkgname=afdko
pkgver=3.2.0
pkgrel=1
pkgdesc="Adobe Font Development Kit for OpenType"
arch=('x86_64')
url="https://adobe-type-tools.github.io/afdko"
license=('Apache')
depends=('python-lxml' 'python-booleanoperations' 'python-cu2qu' 'python-mutatormath'
         'psautohint' 'python-ufonormalizer' 'python-ufoprocessor' 'lib32-glibc')
makedepends=('git' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/adobe-type-tools/afdko.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
	cd "$srcdir/$pkgname"
	sed -i 's/ufoProcessor>=1.0.5,<=1.0.6/ufoProcessor>=1.0.6/g' requirements.txt
}

build() {
	cd "$srcdir/$pkgname"
	python setup.py build
}

check() {
	cd "$srcdir/$pkgname"
	python setup.py test
}

package() {
	cd "$srcdir/$pkgname"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
