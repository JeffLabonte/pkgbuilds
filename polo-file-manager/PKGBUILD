# Maintainer: Your Name <youremail@domain.com>
pkgname=polo-file-manager
_pkgname=polo
pkgver=18.8.2
_pkgver=18.8-beta
pkgrel=1
pkgdesc="Advanced file manager for Linux written in Vala."
arch=('any')
url="https://github.com/teejee2008/polo"
license=('GPL')
depends=('libgee' 'gtk3' 'vte3' 'rsync' 'ffmpeg' 'mediainfo' 'p7zip' 'fish' 'qemu' 'gvfs' 'wget')
makedepends=('vala')
optdepends=('clamscan' 'imagemagick' 'diffuse' 'perl-image-exiftool' 'gnome-disk-utility' 'ghostscript' 'lzop' 'pdftk' 'pngcrush' 'pv' 'rclone' 'udisks2' 'xdg-utils' 'youtube-dl')
source=($pkgname-$pkgver.tar.gz::"$url/archive/v$_pkgver.tar.gz")
md5sums=('e260c26ce6348328fb472512da61f37d')

prepare() {
	cd "$_pkgname-$_pkgver/src/Gtk"
	sed -i 's/term.feed_child(cmd, -1);/term.feed_child(cmd.to_utf8());/g' TermBox.vala
}

build() {
	cd "$_pkgname-$_pkgver"
	make all
}

package() {
	cd "$_pkgname-$_pkgver"
	make DESTDIR="$pkgdir/" install
}
