_name=Solaar
pkgname=solaar
pkgver=1.0.0
pkgrel=1
pkgdesc="Device manager for Logitech's Unifying receiver peripherals"
url="https://github.com/pwr-Solaar/Solaar"
license=('GPL2')
arch=('any')
depends=('gtk3' 'libnotify' 'python-dbus' 'python-gobject' 'python-pyudev')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwr/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('ce55a022d2183c7be47e2298d3a45ce5ab0f69bc53ef1e1976104f77b1f69d876cf97a62620225cbf280eed08a086ce3ef39f9aa5e8470e53b6d4d62d5d94f13')

build() {
	cd "${_name}-${pkgver}"
	python setup.py build
}

package() {
	cd "${_name}-${pkgver}"
	python setup.py install --skip-build \
		--optimize=1 \
		--prefix=/usr \
		--root="${pkgdir}/"
	# udev
	install -vDm 644 rules.d/42-logitech-unify-permissions.rules \
		"${pkgdir}/usr/lib/udev/rules.d/42-logitech-unify-permissions.rules"
	# docs
	install -vDm 644 {ChangeLog,README.md} \
		-t "${pkgdir}/usr/share/doc/${pkgname}/"
	# Fix typo #534: https://github.com/pwr-Solaar/Solaar/pull/534/commits/5e35482d51ff1f01c7a34c611525f720eed52a35
	sed -i 's/slef.product_id/self.product_id/g' \
		$pkgdir/usr/lib/python3.7/site-packages/logitech_receiver/receiver.py
}
