# https://aur.archlinux.org/packages/auto-cpufreq-git
pkgname=auto-cpufreq-git
pkgver=1.1.2.r3.gabb66ed
pkgrel=1
pkgdesc="Automatic CPU speed & power optimizer"
arch=('any')
url="https://foolcontrol.org/?p=3124"
license=('LGPL3')
depends=('python-distro' 'python-psutil' 'python-click' 'python-power')
makedepends=('git' 'python-setuptools')
optdepends=('cpufreqctl: CPU Power Manager'
            'gnome-shell-extension-cpufreq: CPU Power Manager for GNOME Shell')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install="${pkgname%-git}.install"
source=('git+https://github.com/AdnanHodzic/auto-cpufreq.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	sed -i 's|usr/local|usr|g' "scripts/${pkgname%-git}.service"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm755 scripts/cpufreqctl.sh "$pkgdir/usr/bin/cpufreqctl"
	install -Dm644 "scripts/${pkgname%-git}.service" -t \
		"$pkgdir/usr/lib/systemd/system"
}
