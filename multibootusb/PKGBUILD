pkgname=multibootusb
pkgver=9.2.0
pkgrel=4
pkgdesc="Boot multiple live Linux distros from a usb flash drive."
arch=('any')
url="http://multibootusb.org"
license=('GPL')
depends=('python-pyqt5' 'python-dbus' 'mtools' 'parted' 'p7zip' 'python-pyudev' 'lsof')
sha256sums=('1f1539e11e5ac8af2fc2379a22c2ad6b73759b2babbc165f7ff716240e922d7d')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mbusb/$pkgname/archive/v$pkgver.tar.gz")

build() {
	cd "$pkgname-$pkgver"
	python setup.py build
}

package() {
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
	sed -i 's/\/usr\/local\/bin/\/usr\/bin/' \
		"$pkgdir/usr/share/polkit-1/actions/org.debian.pkexec.run-multibootusb.policy"
}
