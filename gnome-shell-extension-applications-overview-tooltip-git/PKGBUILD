pkgname=gnome-shell-extension-applications-overview-tooltip-git
_pkgname=applications-overview-tooltip
pkgver=10.r0.g4404f67
pkgrel=1
pkgdesc="Shows a tooltip over applications icons on applications overview."
arch=('any')
url="https://github.com/RaphaelRochet/applications-overview-tooltip"
license=('none')
depends=('gnome-shell')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/RaphaelRochet/applications-overview-tooltip.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	_uuid="${_pkgname}@RaphaelRochet"

	cd "$srcdir/$_pkgname"
	install -d "$pkgdir/usr/share/gnome-shell/extensions/$_uuid"
	cp -r * "$pkgdir/usr/share/gnome-shell/extensions/$_uuid"
}
