pkgname=('nautilus-columns-git' 'nemo-columns-git' 'caja-columns-git')
pkgver=r5.79310bb
pkgrel=1
arch=('any')
url="https://github.com/atareao/nautilus-columns"
license=('GPL3')
source=('git+https://github.com/atareao/nautilus-columns.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgbase%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package_nautilus-columns-git() {
	pkgdesc="Adds metadata columns to Nautilus"
	depends=('python-nautilus' 'python-pillow' 'python-mutagen' 'python-exiv2' 'kaa-metadata'
	         'python-pypdf2')
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/${pkgname%-git}"
	install -Dm644 src/*.py -t "$pkgdir/usr/share/nautilus-python/extensions"

	# Create language directories
	install -dm755 "$pkgdir/usr/share/locale"/{es,gl}/LC_MESSAGES

	# Compile languages
	msgfmt po/es.po -o "$pkgdir/usr/share/locale/es/LC_MESSAGES/${pkgname%-git}.mo"
	msgfmt po/gl.po -o "$pkgdir/usr/share/locale/gl/LC_MESSAGES/${pkgname%-git}.mo"
}

package_nemo-columns-git() {
	pkgdesc="Adds metadata columns to Nemo"
	depends=('nemo-python' 'python-pillow' 'python-mutagen' 'python-exiv2' 'kaa-metadata'
	         'python-pypdf2')
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/nautilus-columns"
	install -Dm644 src/*.py -t "$pkgdir/usr/share/nemo-python/extensions"

	# Create language directories
	install -dm755 "$pkgdir/usr/share/locale"/{es,gl}/LC_MESSAGES

	# Compile languages
	msgfmt po/es.po -o "$pkgdir/usr/share/locale/es/LC_MESSAGES/${pkgname%-git}.mo"
	msgfmt po/gl.po -o "$pkgdir/usr/share/locale/gl/LC_MESSAGES/${pkgname%-git}.mo"
}

package_caja-columns-git() {
	pkgdesc="Adds metadata columns to Caja"
	depends=('python-caja' 'python-pillow' 'python-mutagen' 'python-exiv2' 'kaa-metadata'
	         'python-pypdf2')
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/nautilus-columns"
	install -Dm644 src/*.py -t "$pkgdir/usr/share/caja-python/extensions"

	# Create language directories
	install -dm755 "$pkgdir/usr/share/locale"/{es,gl}/LC_MESSAGES

	# Compile languages
	msgfmt po/es.po -o "$pkgdir/usr/share/locale/es/LC_MESSAGES/${pkgname%-git}.mo"
	msgfmt po/gl.po -o "$pkgdir/usr/share/locale/gl/LC_MESSAGES/${pkgname%-git}.mo"
}
