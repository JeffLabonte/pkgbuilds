# https://www.archlinux.org/packages/community/x86_64/lsd/

pkgname=lsd
pkgver=0.18.0
pkgrel=1
pkgdesc='Modern ls with a lot of pretty colors and awesome icons'
url='https://github.com/Peltoche/lsd'
arch=('x86_64')
license=('Apache')
depends=('gcc-libs' 'ttf-nerd-fonts-symbols')
makedepends=('rust')
source=("https://github.com/Peltoche/lsd/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('2905e5d8d61d98d60c65544f3d41ae77459c8a92a4c7acafadcfb0233b2b7f95')

build() {
	cd "$pkgname-$pkgver"
	SHELL_COMPLETIONS_DIR=completions cargo build --release --locked
}

check() {
	cd "$pkgname-$pkgver"
	cargo test --release --locked
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 "target/release/$pkgname" -t "${pkgdir}/usr/bin"
	install -Dm644 README.md CHANGELOG.md -t "$pkgdir/usr/share/doc/$pkgname"

	# shell completions
	install -Dm644 "completions/$pkgname.bash" \
		"$pkgdir/usr/share/bash-completion/completions/$pkgname"
	install -Dm644 "completions/$pkgname.fish" \
		"$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
	install -Dm644 "completions/_$pkgname" \
		"$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
