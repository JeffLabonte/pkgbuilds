pkgname=devilutionx
pkgver=1.0.1
pkgrel=1
pkgdesc="Diablo build for modern operating systems"
arch=('x86_64')
url="https://github.com/diasurgical/devilutionX"
license=('custom:Unlicense' 'custom:OFL')
depends=('graphite' 'hicolor-icon-theme' 'libsodium' 'sdl2_mixer' 'sdl2_ttf')
makedepends=('git' 'cmake' 'gcc-libs')
source=("$pkgname::git+$url.git#tag=$pkgver"
        'DIABDAT.MPQ') # Place in build dir
sha256sums=('SKIP'
            '63fb47d9c76484024c7640d90ab6b7ec5e13f567a7e1a917b6c03a6631d3f2b0')

prepare() {
	cd "$srcdir/$pkgname"
	mkdir -p build
}

build() {
	cd "$srcdir/$pkgname/build"
	cmake .. \
		-DPIE=ON \
		-DBINARY_RELEASE=ON
	make
}

package() {
	cd "$srcdir/$pkgname"

	# Install and link binary
	install -Dm755 "build/$pkgname" -t "$pkgdir/usr/share/diasurgical/$pkgname"
	install -dm755 "$pkgdir/usr/bin"
	ln -s "/usr/share/diasurgical/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"

	# Install game data file
	install -m644 "$srcdir/DIABDAT.MPQ" \
		"$pkgdir/usr/share/diasurgical/$pkgname/diabdat.mpq"

	# Install font
	install -m644 Packaging/resources/CharisSILB.ttf -t \
		"$pkgdir/usr/share/diasurgical/$pkgname"

	# Install icons
	install -Dm644 Packaging/resources/16.png \
		"$pkgdir/usr/share/icons/hicolor/16x16/apps/$pkgname.png"
	install -Dm644 Packaging/resources/Diablo_32.png \
		"$pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png"
	install -Dm644 Packaging/resources/Diablo_48.png \
		"$pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png"
	install -Dm644 Packaging/cpi-gamesh/Devilution.png \
		"$pkgdir/usr/share/icons/hicolor/256x256/apps/$pkgname.png"

	# Install desktop file
	install -Dm664 Packaging/fedora/$pkgname.desktop -t \
		"$pkgdir/usr/share/applications"

	# Install docs
	install -Dm644 docs/*.md -t "$pkgdir/usr/share/doc/$pkgname"

	# Install license
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"

	# Install font license
	install -Dm644 Packaging/resources/LICENSE.CharisSILB.txt -t \
		"$pkgdir/usr/share/licenses/$pkgname"
}
