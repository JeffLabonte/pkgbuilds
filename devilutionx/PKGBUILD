# https://aur.archlinux.org/packages/devilutionx
pkgname=devilutionx
pkgver=1.1.0
pkgrel=2
pkgdesc="Diablo build for modern operating systems"
arch=('x86_64')
url="https://github.com/diasurgical/devilutionX"
license=('Unlicense')
depends=('graphite' 'hicolor-icon-theme' 'libsodium' 'sdl2_mixer' 'sdl2_ttf')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('395ec298df5383abe60550d45adba5e5811984589ccb1e504891b267b3c467c1')

build() {
	cmake -B build -S devilutionX-$pkgver \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DPIE=ON \
		-DBINARY_RELEASE=ON \
		-Wno-dev
	make -C build
}

package() {
	make -C build DESTDIR="$pkgdir" install

	cd devilutionX-$pkgver

	# Install docs
	install -Dm644 docs/*.md -t "$pkgdir/usr/share/doc/$pkgname"
	install -Dm644 docs/manual/platforms/*.md -t \
		"$pkgdir/usr/share/doc/$pkgname/manual/platforms"

	mv "$pkgdir/usr/share/diasurgical/$pkgname/README.txt" \
		"$pkgdir/usr/share/doc/$pkgname"
	rm -rf "$pkgdir/usr/share/diasurgical"

	# Install license
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
