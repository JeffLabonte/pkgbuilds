# https://aur.archlinux.org/packages/python-googletrans
groups=('modified')

pkgname=python-googletrans
_name=${pkgname#python-}
pkgver=3.0.0
pkgrel=2
pkgdesc="Free Google Translate API for Python."
arch=('any')
url="https://github.com/ssut/py-googletrans"
license=('MIT')
depends=('python-httpx')
makedepends=('python-setuptools')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz"
        "https://raw.githubusercontent.com/ssut/py-googletrans/master/LICENSE")
sha256sums=('44caeea42d91ff6ead5c2d49db2b88de66c45c2fe874c8ec03eb9b4ceb3a533d'
            '58133601df8fe921c7910b43210d724da1675037b33652d90dde101a9fcda197')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	export PYTHONHASHSEED=0
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 "$srcdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}

