pkgname=cheat-git
pkgver=3.0.3.r0.33ac3d3
pkgrel=1
pkgdesc="Allows you to create and view interactive cheatsheets on the command-line"
arch=('x86_64')
url="https://github.com/cheat/cheat"
license=('MIT')
makedepends=('git' 'go-pie')
optdepends=('fzf: for Fuzzy Finder integration')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" 'cheat-bash-git' 'python-cheat')
source=('git+https://github.com/cheat/cheat.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	
	# create gopath
	mkdir -p "$srcdir/gopath"
	export GOPATH="$srcdir/gopath"
	
	# fetch dependencies
	msg "Fetching Go dependencies"
	cd "$srcdir/gopath"
	go get -d ./...
}

build() {
	cd "$srcdir/${pkgname%-git}"
	GOOS=linux \
	GOARCH=amd64 \
	go build \
		-v \
		-trimpath \
		-gcflags "all=-trimpath=$srcdir" \
		-asmflags "all=-trimpath=$srcdir" \
		-ldflags "-extldflags $LDFLAGS" \
		-o "./dist/${pkgname%-git}" "./cmd/${pkgname%-git}"
		
		# Clean mod cache for makepkg -C
		go clean -modcache
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm755 "dist/${pkgname%-git}" "$pkgdir/usr/bin/${pkgname%-git}"
	install -Dm755 scripts/fzf.bash "$pkgdir/usr/share/${pkgname%-git}/fzf.bash"
	install -Dm655 LICENSE.txt "$pkgdir/usr/share/licenses/${pkgname%-git}/LICENSE.txt"
}
