pkgname=yay
pkgver=9.4.6
pkgrel=1
pkgdesc="Yet another yogurt. Pacman wrapper and AUR helper written in go."
arch=('i686' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/Jguer/yay"
license=('GPL')
depends=('pacman>=5.2' 'sudo' 'git')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Jguer/yay/archive/v${pkgver}.tar.gz")
sha256sums=('e2b2d896c641519c9f8d0638d779dea37929238cfac122b31ee6bf509b5c58e7')

build() {
	cd "$pkgname-$pkgver"
	EXTRA_GOFLAGS="-gcflags all=-trimpath=${PWD} -asmflags all=-trimpath=${PWD}" \
	LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\"" \

	make VERSION=$pkgver DESTDIR="$pkgdir" build
}

package() {
	cd "$pkgname-$pkgver"
	make VERSION=$pkgver DESTDIR="$pkgdir" PREFIX=/usr install
}
