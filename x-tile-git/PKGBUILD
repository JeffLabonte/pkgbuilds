# http://aur.archlinux.org/packages/x-tile-git
pkgname=x-tile-git
pkgver=2.6.r1.gec351b1
pkgrel=1
pkgdesc="An application that allows you to select a number of windows and tile them in different ways."
arch=('any')
url="https://www.giuspen.com/x-tile"
license=('GPL2')
depends=('gconf' 'libappindicator-gtk3' 'python2-gobject')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/giuspen/${pkgname%-git}.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	for i in $(find -name '*.py') ${pkgname%-git}; do
		sed -i 's:^#!/usr/bin/env python$:&2:' "$i"
	done
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python2 setup.py install --prefix=/usr --exec-prefix=/usr -f --root="$pkgdir/" --optimize=1
}
