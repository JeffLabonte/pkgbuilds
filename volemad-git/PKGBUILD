pkgname=volemad-git
pkgver=r23.79e7fcc
pkgrel=1
pkgdesc="Common Lisp interface for Mullvad's Wireguard servers"
arch=('any')
url="https://gitlab.com/emacsomancer/volemad"
license=('GPL3')
depends=('sbcl' 'libnotify' 'wireguard-tools' 'yad')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install="${pkgname%-git}.install"
source=('git+https://gitlab.com/emacsomancer/volemad.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"

	# Remove precompiled binary
	rm "${pkgname%-git}"

	make clean
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make all
}

#check() {
#	cd "$srcdir/${pkgname%-git}"
#	make -k check
#}

package() {
	cd "$srcdir/${pkgname%-git}"
	make PREFIX=/usr DESTDIR="$pkgdir/" install

#	install -dm644 "$pkgdir/usr/share/${pkgname%-git}/icons"
#	cp icons/* "$pkgdir/usr/share/${pkgname%-git}/icons"
}
