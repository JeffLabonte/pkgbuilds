pkgname=python-qdarkstyle
_name=QDarkStyle
pkgver=2.8
pkgrel=1
pkgdesc="A dark stylesheet for Python and Qt applications"
arch=('any')
url="https://github.com/ColinDuquesnoy/QDarkStyleSheet"
license=('MIT')
depends=('python-helpdev' 'python-qtpy' 'python-pip')
makedepends=('python-setuptools')
optdepends=('python-qtsass: develop'
            'python-watchdog: develop'
            'python-sphinx: docs'
            'python-sphinx_rtd_theme: docs'
            'python-pyqt5: example'
            'python-pyside2: example')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('6a967c4b664446f8bed9df12d1032cf68cb54f186bfc9cbfdbbc756bf9a5d475')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.rst -t "$pkgdir/usr/share/licenses/$pkgname"
}
