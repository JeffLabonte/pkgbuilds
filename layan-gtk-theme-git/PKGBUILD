pkgname=layan-gtk-theme-git
pkgver=r34.9790ab8
pkgrel=1
pkgdesc="A flat Material Design theme for GTK 3, GTK 2 and Gnome-Shell"
arch=('any')
url="https://github.com/vinceliuice/Layan-gtk-theme"
license=('GPL2')
depends=('gtk3' 'gtk-engine-murrine' 'gtk-engines')
makedepends=('git')
optdepends=('kvantum-theme-layan: Matching Kvantum theme'
            'tela-icon-theme: Recommended icon theme')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/vinceliuice/Layan-gtk-theme.git")
sha256sums=('SKIP')

pkgver(){
	cd "$srcdir/${pkgname%-git}"
	( set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/usr/share/themes"
	./install.sh -d "$pkgdir/usr/share/themes"
}
