pkgname=authy-bin
pkgver=1.8.0
pkgrel=1
pkgdesc="Two factor authentication desktop application"
arch=('x86_64')
url='https://authy.com'
license=('unknown')
depends=('electron')
makedepends=('squashfs-tools')
optdepends=('libappindicator-gtk3: tray icon support') # not yet implemented
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
_snapid="H8ZpNgIoPyvmkgxOWw5MSzsXK1wRZiHn"
_snaprev="1"
source=("${pkgname%-*}-$pkgver.snap::https://api.snapcraft.io/api/v1/snaps/download/${_snapid}_${_snaprev}.snap"
        "${pkgname%-*}.sh")
sha256sums=('6189c2439fbddae802b1f7f86132ffdeeed6f8e911108c41cd353ef1a70858d9'
            '1a9313361f4cde5e8b7883e8a3fc8f0edd7626776ab63f9428beddbf7de45b96')

prepare() {
    unsquashfs -f -d "$srcdir/${pkgname%-*}" "${pkgname%-*}-$pkgver.snap"

	cd "$srcdir/${pkgname%-*}"
    sed -i 's|${SNAP}/meta/gui/icon.png|authy|g' "meta/gui/${pkgname%-*}.desktop"
}

package() {
	cd "$srcdir/${pkgname%-*}"
    install -d "$pkgdir/usr/lib/${pkgname%-*}"
    cp -r resources "$pkgdir/usr/lib/${pkgname%-*}"
    install -Dm755 "$srcdir/${pkgname%-*}.sh" "$pkgdir/usr/bin/${pkgname%-*}"
    install -Dm644 meta/gui/${pkgname%-*}.desktop -t "${pkgdir}/usr/share/applications"
    install -Dm644 meta/gui/icon.png "$pkgdir/usr/share/pixmaps/${pkgname%-*}.png"
}
