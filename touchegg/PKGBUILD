# https://aur.archlinux.org/packages/touchegg
pkgname=touchegg
pkgver=2.0.3
pkgrel=1
pkgdesc="Multitouch gesture recognizer"
arch=('i686' 'x86_64')
url="https://github.com/JoseExposito/touchegg"
license=('GPL')
depends=('systemd-libs' 'libinput' 'pugixml' 'cairo' 'libx11' 'libxtst' 'libxrandr')
makedepends=('cmake')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('0cece713c181fceb1346c737700ef6f195c1dc245f5aa580fda0f878864e13e5')

build() {
	cmake -B build -S "$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-Wno-dev
	make -C build
}

package() {
	make -C build DESTDIR="$pkgdir" install

	[ -d "$pkgdir/lib" ] && mv "$pkgdir/lib" "$pkgdir/usr/lib"
}
