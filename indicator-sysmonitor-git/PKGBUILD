# https://aur.archlinux.org/pkgbase/indicator-sysmonitor-git
pkgname=('indicator-sysmonitor-git' 'budgie-sysmonitor-applet-git')
pkgbase=indicator-sysmonitor-git
pkgver=0.8.3.r108.5062118
pkgrel=1
arch=('any')
url="https://launchpad.net/indicator-sysmonitor"
license=('GPL3')
depends=('python-psutil' 'curl' 'python-gobject')
makedepends=('git')
optdepends=('lm_sensors' 'gnu-netcat' 'hddtemp')
source=('git+https://github.com/fossfreedom/indicator-sysmonitor.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s.r%s.%s" "$(grep 'VERSION = ' preferences.py | head -n1 | cut -d\' -f2)" \
		"$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package_indicator-sysmonitor-git() {
	pkgdesc="Display CPU and memory usage as percents with an indicator applet."
	depends+=('libappindicator-gtk3')
	provides=("${pkgbase%-git}")
	conflicts=("${pkgbase%-git}" "${pkgbase%-git}-bzr" "${pkgbase%-git}-appindicator")

	cd "$srcdir/${pkgbase%-git}"
	make DESTDIR="$pkgdir" install

	python -m compileall -d '/' "$pkgdir"
	python -O -m compileall -d '/' "$pkgdir"
}

package_budgie-sysmonitor-applet-git() {
	pkgdesc="Display CPU and memory usage as percents in a Budgie desktop applet."
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}" "${pkgbase%-git}-budgie")

	cd "$srcdir/${pkgbase%-git}"
	make DESTDIR="$pkgdir" installbudgie

	python -m compileall -d '/' "$pkgdir"
	python -O -m compileall -d '/' "$pkgdir"
}
