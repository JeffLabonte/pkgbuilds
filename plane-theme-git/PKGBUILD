pkgname=plane-theme-git
pkgver=r147.2ceb3d8
pkgrel=1
pkgdesc='A beautiful theme for Gnome Linux'
arch=('any')
url="https://github.com/wfpaisa/plane-theme"
license=('GPL')
depends=('gnome-shell')
makedepends=('git' 'npm' 'gulp')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/wfpaisa/${pkgname%-git}.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	npm install --cache "$srcdir/npm-cache"
	gulp
}

package() {
	install -dm755 "$pkgdir/usr/share/themes"

	cd "$srcdir/${pkgname%-git}/build"
	cp -r Plane "$pkgdir/usr/share/themes/Plane"
	cp -r Plane-dark "$pkgdir/usr/share/themes/Plane-dark"
}

