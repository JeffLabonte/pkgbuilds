# https://gitlab.manjaro.org/packages/community/themes/plane-theme
pkgname=plane-theme-git
pkgver=r166.c503632
pkgrel=1
pkgdesc='A beautiful GTK theme for GNOME and Xfce'
arch=('any')
url="https://www.pling.com/p/1198768"
license=('GPL')
makedepends=('git' 'npm' 'nvm' 'gulp')
optdepends=('plane-icon-theme: matching icon theme'
            'breeze-snow-cursor-theme: recommended cursor theme')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=("git+https://github.com/wfpaisa/plane-theme.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	export npm_config_cache="$srcdir/npm-cache"
	npm config delete prefix
	source /usr/share/nvm/init-nvm.sh
	local npm_prefix=$(npm config get prefix)
	local nodeversion='8.17.0'
	nvm install "$nodeversion" && nvm use "$nodeversion"

	cd "$srcdir/${pkgname%-git}"
	npm install
	gulp

	# Restore node config from nvm
	npm config set prefix "$npm_prefix"
	nvm unalias default
}

package() {
	install -d "$pkgdir/usr/share/themes"

	cd "$srcdir/${pkgname%-git}/build"
	cp -r Plane Plane-dark "$pkgdir/usr/share/themes"
}
