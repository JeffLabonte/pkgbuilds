pkgname=nutty-git
pkgver=143.ad98f83
pkgrel=1
pkgdesc='A network utility that monitors the devices on your network, checks bandwidth and speed details.'
arch=('i686' 'x86_64')
url='https://github.com/babluboy/nutty'
license=('GPL3')
depends=('gtk3' 'granite' 'sqlite' 'libxml2' 'libnotify' 'libgee' 'net-tools' 'nethogs' 'nmap' 'traceroute' 'vnstat' 'curl' 'wireless_tools' 'iproute2' 'pciutils')
makedepends=('vala' 'meson' 'appstream-glib')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install="${pkgname%-*}.install"
source=('git+https://github.com/babluboy/nutty.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd "$srcdir/${pkgname%-git}"
	meson build --prefix=/usr
	mkdir -p build && cd build
	ninja
}

package() {
	cd "$srcdir/${pkgname%-git}/build"
	DESTDIR="$pkgdir/" ninja install
}
