pkgname=nutty-git
pkgver=1.1.1.r27.14229d8
pkgrel=1
pkgdesc='A network utility that monitors the devices on your network,
         checks bandwidth and speed details.'
arch=('i686' 'x86_64')
url='https://github.com/babluboy/nutty'
license=('GPL3')
depends=('granite' 'sqlite' 'libxml2' 'libnotify' 'net-tools' 'nethogs' 'nmap' 'traceroute'
         'vnstat' 'wireless_tools')
makedepends=('git' 'meson' 'vala' 'appstream')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install="${pkgname%-git}.install"
source=('git+https://github.com/babluboy/nutty.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	arch-meson build
	ninja -C build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	DESTDIR="$pkgdir" ninja -C build install
}
