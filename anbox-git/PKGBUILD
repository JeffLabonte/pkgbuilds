pkgname=('anbox-git' 'anbox-modules-dkms-git')
pkgbase=anbox-git
pkgver=r1098.cd829e9
pkgrel=1
epoch=1
arch=('x86_64')
url="https://anbox.io"
license=('GPL3')
makedepends=('cmake' 'git' 'glm' 'lxc' 'sdl2_image' 'protobuf' 'boost' 'properties-cpp' 'gtest' 'python2' 'libdwarf')
source=("git+https://github.com/anbox/anbox.git#commit=cd829e9ccd3a5d654c8aa5e16e32f0d3915d54a8"
	"git+https://github.com/anbox/anbox-modules.git"
	'anbox-container-manager.service'
	'anbox-session-manager.service'
	'99-anbox.rules'
	'anbox.conf'
	'anbox.desktop'
	'anbox-bridge.network'
	'anbox-bridge.netdev')
sha256sums=('SKIP'
            'SKIP'
            '5be94b63dc30d141f15ca7d1be6e3e81f26ef33f844614975537562f5d08236c'
            '1f22dbb5a3ca6925bbf62899cd0f0bbaa0b77c879adcdd12ff9d43adfa61b1d8'
            '210eb93342228168f7bb632c8b93d9bfda6f53f62459a6b74987fa1e17530475'
            '3e07dc524a827c1651857cce28a06c1565bc5188101c140ed213bbafedc5abff'
            '7332d09865be553a259a53819cebddd21f661c7a251d78c2f46acd75c66676b6'
            '44899328725667041e6e84912da81c1d0147b708006eb2c2bb6503f271629ff0'
            '559190df4d6d595480b30d8b13b862081fc4aac52790e33eb24cf7fbcb8003b8')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	( set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	cd "$srcdir/${pkgname%-git}"

	# Don't build tests
	truncate -s 0 cmake/FindGMock.cmake
	truncate -s 0 tests/CMakeLists.txt
}

build() {
	mkdir -p "$srcdir/${pkgname%-git}/build"
	cd "$srcdir/${pkgname%-git}/build"

	cmake .. -DCMAKE_INSTALL_LIBDIR=/usr/lib -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS="-Wno-error=implicit-fallthrough -Wno-error=missing-field-initializers" -DCMAKE_BUILD_TYPE=Release
	make
}

package_anbox-git() {
	pkgdesc="Running Android in a container"
	depends=('lxc' 'sdl2_image' 'protobuf' 'anbox-image' 'libsystemd' 'boost-libs')
	optdepends=('anbox-modules-dkms-git: Required Android kernel modules')
	provides=("${pkgbase%-git}")
	conflicts=("${pkgbase%-git}")

	cd "$srcdir/${pkgname%-git}"
	make -C build DESTDIR="$pkgdir" install

	install -Dm 644 -t $pkgdir/usr/lib/systemd/system $srcdir/anbox-container-manager.service
	install -Dm 644 -t $pkgdir/usr/lib/systemd/user $srcdir/anbox-session-manager.service
	install -Dm 644 $srcdir/anbox-bridge.network \
		$pkgdir/usr/lib/systemd/network/80-anbox-bridge.network
	install -Dm 644 $srcdir/anbox-bridge.netdev \
		$pkgdir/usr/lib/systemd/network/80-anbox-bridge.netdev
	install -Dm 644 -t $pkgdir/usr/lib/udev/rules.d $srcdir/99-anbox.rules
	install -Dm 644 -t $pkgdir/usr/share/applications $srcdir/anbox.desktop
	install -Dm 644 snap/gui/icon.png $pkgdir/usr/share/pixmaps/anbox.png
}

package_anbox-modules-dkms-git() {
	pkgdesc="Required kernel module sources for Android"
	arch=('any')
	depends=('dkms')
	provides=("${pkgname%-git}")
	conflicts=("${pkgname%-git}")

	cd "$srcdir/anbox-modules"
	modules=(ashmem binder)
	for mod in "${modules[@]}"; do
		install -dm 755 $pkgdir/usr/src
		cp -a $mod $pkgdir/usr/src/anbox-modules-$mod-$pkgver
	done;

	install -Dm 644 -t $pkgdir/usr/lib/modules-load.d $srcdir/anbox.conf
}
