# https://aur.archlinux.org/packages/hydrapaper-git
pkgname=hydrapaper-git
pkgver=2.0.2.r1.g1deb325
pkgrel=1
pkgdesc="A Gtk utility to set different backgrounds for each monitor on GNOME"
arch=('any')
url="https://gitlab.gnome.org/gabmus/hydrapaper"
license=('GPL3')
depends=('gtk3' 'python-pillow' 'libhandy' 'python-gobject')
makedepends=('git' 'meson' 'gobject-introspection' 'pandoc')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.gnome.org/gabmus/hydrapaper.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags --always | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${pkgname%-git}" build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
