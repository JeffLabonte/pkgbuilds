# https://www.archlinux.org/packages/community/x86_64/miniupnpc
pkgname=miniupnpc
pkgver=2.1.20191224
pkgrel=1
pkgdesc='Small UPnP client library/tool to access Internet Gateway Devices'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
depends=('sh')
provides=('libminiupnpc.so=17-64')
source=("https://miniupnp.tuxfamily.org/files/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('447b427854b6c027ea28bc838b18582c5a7e71db84f6ff36df93bd91e46d66cf'
            'SKIP')
validpgpkeys=('BEB7EA42900FF505B7907AE50FF11B67A5C0863C')
              # miniupnp (miniupnp !) <miniupnp@free.fr>

build() {
	cd "$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgver"
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm644 man3/miniupnpc.3 -t "$pkgdir/usr/share/man/man3"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
