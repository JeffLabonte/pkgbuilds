pkgname=protontricks
pkgver=1.3
pkgrel=1
pkgdesc="A simple wrapper that does winetricks things for Proton enabled games."
arch=('any')
url="https://github.com/Matoking/protontricks"
license=('GPL3')
depends=('python-vdf' 'winetricks')
optdepends=('zenity: GUI for GNOME desktop')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('206d474e41db7d6a292f3beb13d05167da2c22a339e21e3ea78d817092097e4d')

build() {
	cd "$pkgname-$pkgver"
	python setup.py build
}

package() {
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
