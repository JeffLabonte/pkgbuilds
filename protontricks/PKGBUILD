pkgname=protontricks
pkgver=1.2.5
pkgrel=1
pkgdesc="A simple wrapper that does winetricks things for Proton enabled games."
arch=('any')
url="https://github.com/Matoking/protontricks"
license=('GPL3')
depends=('python-vdf' 'winetricks')
optdepends=('zenity: GUI for GNOME desktop')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('a37c8f6b57c6cad6daf726fbf6e90d8b110d0924ef948e1885aed73a2732f8fb')

build() {
	cd "$pkgname-$pkgver"
	python setup.py build
}

package() {
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
