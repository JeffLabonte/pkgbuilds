pkgname=gnome-shell-extension-gtile-git
pkgver=r203.e7ef0ad
pkgrel=1
pkgdesc='A window tiling extension for Gnome'
arch=('any')
url="https://github.com/gTile/gTile"
license=('GPL')
depends=('gnome-shell')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/gTile/gTile.git"
        '119.patch')
sha256sums=('SKIP'
            'aa355b78e5d3b1c36aae91d19c9752e4feedfe5270147411b6db0db6ac98e8f5')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	git am "$srcdir/119.patch"

	sed -i 's/"version": 32/"version": 33/g' metadata.json
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/usr/share/gnome-shell/extensions/gTile@vibou"
	cp -r . "$pkgdir/usr/share/gnome-shell/extensions/gTile@vibou"
}
