pkgname=music21
_name=${pkgname#python-}
pkgver=5.7.2
pkgrel=1
pkgdesc="A toolkit for computer-aided musical analysis"
arch=('any')
url="http://web.mit.edu/music21"
license=('BSD')
depends=('python-chardet' 'python-joblib' 'python-more-itertools' 'python-webcolors')
makedepends=('python-setuptools')
optdepends=('python-matplotlib: Graphing and visual displays support'
            'python-scipy: Scientific extensions to Python'
            'python-pyaudio: For the audioSearch module'
            'python-pygame: Allows for realtime MIDI performance'
            'python-pillow: Resources for transforming and editing graphics files'
            'musescore: Highly recommended for working with music21'
            'lilypond: Automatically generate PDF or PNG files'
            'python-jsonpickle')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('360d977254c4c024da6cffbf7d4fb1ca36ba36ee516b1719a80e7141299bc8a6')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
