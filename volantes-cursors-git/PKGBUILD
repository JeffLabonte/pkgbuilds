# https://aur.archlinux.org/packages/volantes-cursors-git
pkgname=volantes-cursors-git
pkgver=r4.f5dd936
pkgrel=1
pkgdesc="Classic cursor with a flying style."
arch=('any')
url="https://www.pling.com/p/1356095"
license=('GPL2')
makedepends=('git' 'inkscape' 'xorg-xcursorgen')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://github.com/varlesh/volantes-cursors.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/${pkgname%-git}"
    make build
}

package() {
    cd "$srcdir/${pkgname%-git}"
    make DESTDIR="$pkgdir/" install
}
