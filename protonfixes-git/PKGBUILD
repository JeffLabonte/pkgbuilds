# http://aur.archlinux.org/packages/protonfixes-git
pkgname=protonfixes-git
pkgver=1.0.15.r98.g71da369
pkgrel=1
pkgdesc="A module for applying fixes at runtime to unsupported games with Steam Proton without changing game installation files"
arch=('any')
url="https://simons-public.github.io/protonfixes"
license=('BSD')
depends=('python')
makedepends=('git' 'python-setuptools')
optdepends=('wine: win32 proton prefix support'
            'winetricks: winetricks support'
            'python-kivy: Progress Dialog / Splashscreen'
            'kdialog: Progress Dialog / Splashscreen'
            'zenity: Progress Dialog / Splashscreen')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/simons-public/protonfixes.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
}
