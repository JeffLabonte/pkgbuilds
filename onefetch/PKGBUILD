# https://aur.archlinux.org/packages/onefetch
pkgname=onefetch
pkgver=2.6.0
pkgrel=1
pkgdesc="Git repository summary on your terminal"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/o2sh/onefetch"
license=('MIT')
depends=('libgit2')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('2169d5837143e0d16880a656545296874d060e65a6d40cf5d15f902b70cbcb6d')

build() {
	cd "$pkgname-$pkgver"
	cargo build --release --locked --all-features --target-dir=target
}

check() {
	cd "$pkgname-$pkgver"
	cargo test --release --locked --target-dir=target
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
