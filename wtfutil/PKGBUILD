pkgname=wtfutil
pkgver=0.22.0
pkgrel=1
pkgdesc="Personal information dashboard for your terminal"
arch=('i686' 'x86_64' 'aarch64' 'armv6h')
url="https://wtfutil.com"
license=('MPL2')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wtfutil/wtf/archive/v$pkgver.tar.gz")
sha256sums=('b421af6aceba9b4f4ff67d591fd899e5357891a06fa13704ec4e77f983ee1064')

prepare() {
	mkdir -p gopath/src/github.com/$pkgname
	ln -rTsf wtf-$pkgver gopath/src/github.com/$pkgname/wtf
}

build() {
	cd gopath/src/github.com/$pkgname/wtf
	go build -o bin/wtfutil \
		-trimpath \
		-ldflags "-extldflags ${LDFLAGS}" \
		.
}

package() {
	cd "wtf-$pkgver"
	install -Dm755 "bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
	install -Dm644 README.md "$pkgdir/usr/share/docs/$pkgname/README.md"
}
