# https://aur.archlinux.org/packages/wtfutil
pkgname=wtfutil
pkgver=0.32.0
pkgrel=1
pkgdesc="Personal information dashboard for your terminal"
arch=('x86_64' 'aarch64' 'armv6h')
url="https://wtfutil.com"
license=('MPL2')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wtfutil/wtf/archive/v$pkgver.tar.gz")
sha256sums=('4dd3287cd539d503c8d244efd37a4a69d806bdb90c4db0955822e081877cf5e5')

prepare() {
	export GOPATH="$srcdir/gopath"
	go clean -modcache

	# Set the Go proxy
	export GOPROXY="https://proxy.golang.org,direct"

	# Disable the Go checksum database
	export GOSUMDB=off

	# Enable Go modules
	export GO111MODULE=on
}

build() {
	cd "wtf-$pkgver"
	go build \
		-trimpath \
		-buildmode=pie \
		-mod=readonly \
		-modcacherw \
		-ldflags "-extldflags \"${LDFLAGS}\" -s -w -X 'main.version=$pkgver' -X 'main.date=$(date)'" \
		-v -o $pkgname .

	# Clean now to ensure makepkg --clean works
	go clean -modcache
}

package(){
	cd "wtf-$pkgver"
	install -Dm755 "$pkgname" -t "$pkgdir/usr/bin"
	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 README.md CHANGELOG.md -t "$pkgdir/usr/share/doc/$pkgname"
	cp -r _sample_configs "$pkgdir/usr/share/doc/$pkgname/sample_configs"
}
