# https://aur.archlinux.org/packages/librnnoise
pkgname=librnnoise-git
pkgver=r84.125d8a5
pkgrel=1
pkgdesc="A noise suppression library based on a recurrent neural network."
arch=('x86_64')
url="https://github.com/xiph/rnnoise"
license=('BSD')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/xiph/rnnoise.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/rnnoise"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/rnnoise"
	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/rnnoise"
	make DESTDIR="$pkgdir" install

	install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
