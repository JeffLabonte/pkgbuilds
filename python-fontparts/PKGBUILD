# http://aur.archlinux.org/packages/python-fontparts
pkgname=python-fontparts
_name=fontParts
pkgver=0.9.6
pkgrel=1
pkgdesc="The replacement for RoboFab."
arch=('any')
url="https://github.com/robotools/fontParts"
license=('MIT')
depends=('python-fontmath' 'python-defcon' 'python-booleanoperations' 'python-fs')
makedepends=('python-setuptools')
checkdepends=('python-fontpens')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('3fed62d7390735fcfe58fa75f559886b5bed2c4c06dbd045645ad5eb03a31161')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver/Lib"
	PYTHONPATH=. python fontParts/fontshell/test.py
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

