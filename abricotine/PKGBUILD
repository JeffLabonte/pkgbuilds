pkgname=abricotine
_pkgname=Abricotine
pkgver=0.7.0
pkgrel=1
pkgdesc="Markdown editor with inline preview"
arch=('i686' 'x86_64')
url="http://abricotine.brrd.fr"
license=('GPL3')
depends=('gtk3' 'libnotify' 'nss' 'libxss' 'libxtst' 'xdg-utils' 'trash-cli' 'gvfs'
         'pulseaudio' 'alsa-lib') #'kde-cli-tools' 'kdebase-runtime'
makedepends=('npm')
optdepends=('gnome-keyring' 'lsb-release')
source=("$pkgname-$pkgver.tar.gz::https://github.com/brrd/Abricotine/archive/$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('03bc7735be08ceacb412b1fd96d13b8ba14eda629cd8b6c705e8421ee7875442'
            'de31f395f39a50b7210692cddffd43ea3bf944fa038a315bebbfe3e3a79034de')

build() {
	cd "$_pkgname-$pkgver"
	npm install --cache "$srcdir/npm-cache"
	npm run packager
}

package() {
	cd "$_pkgname-$pkgver"
	install -d "$pkgdir/opt/$pkgname"
	cp -r dist/*/* "$pkgdir/opt/$pkgname"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/$pkgname/$_pkgname" "$pkgdir/usr/bin/$pkgname"

	install -Dm644 "../$pkgname.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "icons/$pkgname@2x.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
