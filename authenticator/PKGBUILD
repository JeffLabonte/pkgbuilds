pkgname=authenticator
pkgver=3.32.2
pkgrel=4
pkgdesc="2FA code generator for GNOME"
arch=('x86_64')
url="https://gitlab.gnome.org/World/Authenticator"
license=("GPL3")
depends=('appstream-glib' 'libhandy' 'libsecret' 'python-pyotp' 'python-requests' 'pyzbar'
         'python-pyfavicon' 'python-yoyo-migrations')
makedepends=('git' 'meson' 'gobject-introspection')
source=("$pkgname::git+$url.git?unsigned#tag=$pkgver"
        'git+https://gitlab.gnome.org/GNOME/libgd.git')
sha256sums=('SKIP'
            'SKIP')

prepare() {
    cd "$srcdir/$pkgname"
    git submodule init
    git config --local submodule.subprojects/libgd.url "$srcdir/libgd"
    git submodule update
}

build() {
	cd "$srcdir/$pkgname"
    arch-meson build
    ninja -C build
}

package() {
	cd "$srcdir/$pkgname"
    DESTDIR="$pkgdir" ninja -C build install
}
