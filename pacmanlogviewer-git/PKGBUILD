pkgname=pacmanlogviewer-git
pkgver=1.4.1.r9.g38d323e
pkgrel=1
pkgdesc="Inspect pacman log files"
arch=('x86_64')
url="https://opendesktop.org/content/show.php?content=150484"
license=('GPL')
depends=('qt5-base' 'hicolor-icon-theme')
makedepends=('git' 'cmake' 'qt5-tools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/gcala/pacmanlogviewer.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_WITH_QT5=ON .
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" install
}
