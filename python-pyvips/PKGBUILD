# https://aur.archlinux.org/packages/python-pyvips
pkgname=python-pyvips
_name=${pkgname#python-}
pkgver=2.1.13
pkgrel=1
pkgdesc="Python binding for libvips using cffi"
arch=('x86_64')
url="https://github.com/libvips/pyvips"
license=('MIT')
depends=('libvips' 'python-cffi')
makedepends=('python-setuptools')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('4f1f6fa19893048bf6b950eb7eb2d4cdfeb8b940a9defaca5d4f79e5acd5085f')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	export PYTHONHASHSEED=0
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
