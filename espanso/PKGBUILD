pkgname=espanso
pkgver=0.3.3
pkgrel=1
pkgdesc="Cross-platform text expander written in Rust"
arch=('x86_64')
url="https://espanso.org"
license=('GPL3')
depends=('xdotool' 'xclip' 'libxtst')
makedepends=('git' 'rust' 'cmake')
install="$pkgname.install"
source=("$pkgname::git+https://github.com/federico-terzi/espanso.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
    cd "$srcdir/$pkgname"

    # don't change the original service file, as it will be embedded in the binary
    cp "src/res/linux/systemd.service" "systemd.service"
    sed -i "s|{{{espanso_path}}}|/usr/bin/espanso|g" "systemd.service"
}

build() {
    cd "$srcdir/$pkgname"
    cargo build --release --locked
}

check() {
    cd "$srcdir/$pkgname"
    cargo test --release --locked
}

package() {
    cd "$srcdir/$pkgname"
    install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dm644 "systemd.service" "$pkgdir/usr/lib/systemd/user/$pkgname.service" # install our own copy

    install -Dm644 "README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}
