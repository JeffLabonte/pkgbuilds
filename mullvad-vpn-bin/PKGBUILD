pkgname=mullvad-vpn-bin
_pkgname=mullvad-vpn
pkgver=2018.4
pkgrel=3
pkgdesc="VPN Client for Mullvad.net, a bitcoin-friendly VPN for anonymity and privacy"
url="https://www.mullvad.net"
arch=('x86_64')
license=('GPL3')
depends=('gconf' 'gtk3' 'libnotify' 'libappindicator-gtk2' 'libxtst' 'nss' 'libxss')
conflicts=('mullvad-vpn' 'mullvad-vpn-old')
replaces=('mullvad-vpn-old')
install=$pkgname.install
source=("https://github.com/mullvad/mullvadvpn-app/releases/download/${pkgver}/MullvadVPN-${pkgver}_x86_64.rpm"{,.asc}
        "https://github.com/mullvad/mullvadvpn-app/blob/master/LICENSE.md"
        "mullvad-daemon.service")
sha256sums=('015e1d5276ef8fd1922cfc2f40ab5c00a92f41f451089e0dcb68c0e56bdb4f87'
            'SKIP'
            'b2081f103a2b6b37d5796865bfb9ff052d30b97f938da9f2a7128d2ce55542f6'
            '77b71d613ef09c6ffbcfa66a59f787246f64d4463843e648edc6fbbea18dd55e')
validpgpkeys=('A1198702FC3E0A09A9AE5B75D5A1D4F266DE8DDF')

package() {

    #install systemd service
    install -Dm644 mullvad-daemon.service "${pkgdir}/usr/lib/systemd/system/mullvad-daemon.service"

    # Install the main files.
    install -d "${pkgdir}/opt/${_pkgname}"
    cp -a "${srcdir}/opt/Mullvad VPN/." "${pkgdir}/opt/${_pkgname}"

    # Desktop Entry
    install -d "${pkgdir}/usr/share/applications"
    install "${srcdir}/usr/share/applications/${_pkgname}.desktop" "${pkgdir}/usr/share/applications"
    sed -i 's%opt/Mullvad VPN%usr/bin%g' ${pkgdir}/usr/share/applications/${_pkgname}.desktop

    # CLI binary
    install -d "${pkgdir}/usr/bin"
    cp "${srcdir}/usr/bin/mullvad" "${pkgdir}/usr/bin"
    chmod 755 "${pkgdir}/usr/bin/mullvad"

    # Main binary
    chmod 755 "${pkgdir}/opt/${_pkgname}/${_pkgname}"
    ln -s "/opt/${_pkgname}/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"

    #icons
    install -dm755 "${pkgdir}/usr/share/icons/hicolor"
    cp -a "${srcdir}/usr/share/icons/hicolor/." "${pkgdir}/usr/share/icons/hicolor"

    # License
    install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE.md"

}
