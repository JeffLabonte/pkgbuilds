pkgname=mullvad-vpn-bin
pkgver=2019.6
pkgrel=1
pkgdesc="VPN Client for Mullvad.net, a bitcoin-friendly VPN for anonymity and privacy"
url="https://www.mullvad.net"
arch=('x86_64')
license=('GPL3')
depends=('gconf' 'gtk3' 'libnotify' 'libappindicator-gtk2' 'libxtst' 'nss' 'libxss')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
install=$pkgname.install
source=("https://github.com/mullvad/mullvadvpn-app/releases/download/$pkgver/MullvadVPN-${pkgver}_${arch}.rpm"{,.asc}
        "mullvad-daemon.service")
sha256sums=('7c1bc977727313e5faf7605a88df4e9c15fa4a0321f14366b98008db6963a318'
            'SKIP'
            '77b71d613ef09c6ffbcfa66a59f787246f64d4463843e648edc6fbbea18dd55e')
validpgpkeys=('A1198702FC3E0A09A9AE5B75D5A1D4F266DE8DDF')

package() {

    # Systemd service
    install -Dm644 mullvad-daemon.service "$pkgdir/usr/lib/systemd/system/mullvad-daemon.service"

    # Main files
    install -d "$pkgdir/opt/${pkgname%-bin}"
    cp -a opt/Mullvad VPN/. "$pkgdir/opt/${pkgname%-bin}"

    # Desktop Entry
    install -Dm644 "usr/share/applications/${pkgname%-bin}.desktop" "$pkgdir/usr/share/applications/${pkgname%-bin}.desktop"
    sed -i 's%opt/Mullvad VPN%opt/mullvad-vpn%g' "$pkgdir/usr/share/applications/${pkgname%-bin}.desktop"

    # CLI binary
    install -Dm755 usr/bin/mullvad "$pkgdir/usr/bin/mullvad"

    # Main binary
    chmod 755 "$pkgdir/opt/${pkgname%-bin}/${pkgname%-bin}"
    ln -s "/opt/${pkgname%-bin}/${pkgname%-bin}" "$pkgdir/usr/bin/${pkgname%-bin}"

    # Icons
    install -dm755 "$pkgdir/usr/share/icons/hicolor"
    cp -a usr/share/icons/hicolor/. "$pkgdir/usr/share/icons/hicolor/"
}
