pkgname=chkservice
pkgver=0.3
pkgrel=1
pkgdesc="Systemd units manager with ncurses, terminal interface"
arch=('i686' 'x86_64')
url="https://github.com/linuxenko/chkservice"
license=('GPL')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('56037fd82be06d6115d0738439c23faa291d036ffe33fe590b484b045d6d180d')

prepare() {
	cd "$pkgname-$pkgver"
	mkdir build
}

build() {
	cd "$pkgname-$pkgver/build"
	cmake -DCMAKE_INSTALL_PREFIX=/usr ../
	make
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 "build/src/$pkgname" -t "$pkgdir/usr/bin"
}
