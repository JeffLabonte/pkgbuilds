pkgname=dxvk-bin
pkgver=1.5.1
pkgrel=1
pkgdesc="Vulkan-based implementation of D3D9, D3D10 and D3D11 for Linux / Wine"
arch=('i686' 'x86_64')
url="https://github.com/doitsujin/dxvk"
license=('zlib/libpng')
depends=('vulkan-icd-loader' 'wine>=3.10' 'lib32-vulkan-icd-loader')
provides=("${pkgname%-bin}" "${pkgname%-bin}=$pkgver" 'd9vk')
conflicts=("${pkgname%-bin}" "${pkgname%-bin}=$pkgver" 'd9vk')
options=('!strip')
source=("$url/releases/download/v$pkgver/${pkgname%-bin}-$pkgver.tar.gz")
sha256sums=('474ce9995edd47a3bd347a8f3263f35cf8df2676f5b16668bf38efa298d75c01')

package() {
	cd "${pkgname%-bin}-$pkgver"
	install -D x32/* -t "$pkgdir/usr/share/${pkgname%-bin}/x32"
	install -D x64/* -t "$pkgdir/usr/share/${pkgname%-bin}/x64"
	install -D "setup_${pkgname%-bin}.sh" -t "$pkgdir/usr/share/${pkgname%-bin}"

	install -d "$pkgdir/usr/bin"
	ln -s "/usr/share/${pkgname%-bin}/setup_${pkgname%-bin}.sh" \
		"$pkgdir/usr/bin/setup_${pkgname%-bin}"
}
