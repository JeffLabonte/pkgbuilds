# https://aur.archlinux.org/packages/python-dbus-next/
pkgname=python-dbus-next
_name=dbus_next
pkgver=0.2.2
pkgrel=1
pkgdesc="The next great DBus library for Python with asyncio support"
arch=('any')
url="https://github.com/altdesktop/python-dbus-next"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('f656a3d3450b670f228248ffb1c3a703a69c4a8cb10cce63b108f17c8bd6c3de')

build() {
    cd "$_name-$pkgver"
    python setup.py build
}

package() {
    cd "$_name-$pkgver"
    export PYTHONHASHSEED=0
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
