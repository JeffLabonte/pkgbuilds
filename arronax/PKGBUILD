# https://aur.archlinux.org/packages/arronax
pkgname=arronax
pkgver=0.7.2
pkgrel=1
pkgdesc='A program to create and modify desktop files.'
arch=('any')
license=('GPL3')
url="https://www.florian-diesch.de/software/arronax"
depends=('gtk3' 'python-gobject' 'python-xdg')
makedepends=('python-setuptools')
optdepends=('python-nautilus: for the Nautilus plugin'
            'nemo-python: for the Nemo plugin'
            'python-caja: for the Caja plugin')
source=("https://launchpad.net/~diesch/+archive/ubuntu/stable/+sourcefiles/arronax/$pkgver/${pkgname}_${pkgver}.tar.xz")
sha256sums=('c582123536f85e62d48f35ebfaeb41925a105d93247c57c0139447178e098092')

prepare() {
	# Fix icon location
	cd trunk
	sed -i 's|{s}x{s}|{s}x{s}/apps|g' setup.py
}

build() {
	cd trunk
	python setup.py build
}

package() {
	cd trunk
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}

