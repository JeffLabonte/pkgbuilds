# https://aur.archlinux.org/packages/vkbasalt
pkgname=lib32-vkbasalt
pkgver=0.3.2.3
pkgrel=4
pkgdesc="A Vulkan post processing layer to enhance the visual graphics of games"
arch=('x86_64')
url="https://github.com/DadSchoorse/vkBasalt"
license=('zlib')
depends=('vkbasalt' 'lib32-gcc-libs' 'lib32-libx11')
makedepends=('meson' 'glslang')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('8cc65a69a20ac16802a8ff46629efa6540dee22daeef9db4ad0a22b63d2a4368')

build() {
	export ASFLAGS=--32
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
	arch-meson \
		-Dappend_libdir_vkbasalt=true \
		--libdir=lib32 \
		-Dwith_json=false \
		vkBasalt-$pkgver build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
