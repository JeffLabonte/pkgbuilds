# https://aur.archlinux.org/packages/vkbasalt
pkgname=lib32-vkbasalt
pkgver=0.3.2.4
pkgrel=1
pkgdesc="A Vulkan post processing layer to enhance the visual graphics of games"
arch=('x86_64')
url="https://github.com/DadSchoorse/vkBasalt"
license=('zlib')
depends=('vkbasalt' 'lib32-gcc-libs' 'lib32-libx11')
makedepends=('meson' 'glslang' 'vulkan-headers' 'spirv-headers')
source=("${pkgname#lib32-}-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('f45d513d9f212e9a1e1ec49448e72c5f2510d62303c38fdb5015b9a3afd42b26')

build() {
	export ASFLAGS=--32
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
	arch-meson vkBasalt-$pkgver build \
		-Dappend_libdir_vkbasalt=true \
		--libdir=lib32 \
		-Dwith_json=false
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
