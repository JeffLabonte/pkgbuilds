pkgname=valacef-git
pkgver=76.3809.0.r0.gb1a2163
pkgrel=1
pkgdesc="WIP Vala bindings for Chromium Embedded Framework (CEF)."
arch=('x86_64')
url="https://github.com/tiliado/valacef"
license=('BSD-2-Clause')
depends=('cef=76.1.13' 'python' 'vala' 'glib2' 'gtk3' 'libx11')
makedepends=('git' 'waf' 'python-cppheaderparser')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/tiliado/valacef.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	waf distclean configure \
		--prefix=/usr \
		--libdir=/usr/lib
	waf build -v -p
}

package() {
	cd "$srcdir/${pkgname%-git}"
	waf install --destdir="$pkgdir"

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
}
