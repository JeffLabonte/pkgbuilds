# https://aur.archlinux.org/packages/python-miniupnpc
pkgname=python-miniupnpc
pkgver=2.1.20191224
pkgrel=1
pkgdesc="A small UPnP client library/tool to access Internet Gateway Devices"
arch=('x86_64')
url="https://miniupnp.tuxfamily.org"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("https://miniupnp.tuxfamily.org/files/${pkgname#python-}-$pkgver.tar.gz"{,.sig})
sha256sums=('447b427854b6c027ea28bc838b18582c5a7e71db84f6ff36df93bd91e46d66cf'
            'SKIP')
validpgpkeys=('BEB7EA42900FF505B7907AE50FF11B67A5C0863C')
              # miniupnp (miniupnp !) <miniupnp@free.fr>

build() {
	cd "${pkgbase#python-}-$pkgver"
	python setup.py build
}

package() {
	cd "${pkgbase#python-}-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
