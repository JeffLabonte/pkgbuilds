pkgname=shortwave
_pkgname=Shortwave
pkgver=0.0.1
pkgrel=1
pkgdesc="Find and listen to internet radio stations."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://gitlab.gnome.org/World/Shortwave"
license=('GPL3')
depends=('gstreamer' 'libhandy')
makedepends=('appstream-glib' 'gobject-introspection' 'gst-plugins-base-libs' 'libdazzle' 'meson' 'rust')
provides=('gradio')
conflicts=('gradio')
source=("https://gitlab.gnome.org/World/$_pkgname/-/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha256sums=('7173400e55c8c5d0117ca1e877942af685e646197552f7ee8103fca73d648e34')

build() {
	cd "$_pkgname-$pkgver"
	meson --prefix=/usr build
	ninja -C build
}

check() {
	cd "$_pkgname-$pkgver"
	ninja -C build test
}

package() {
	cd "$_pkgname-$pkgver"
	DESTDIR="$pkgdir/" ninja -C build install
}
