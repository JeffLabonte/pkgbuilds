pkgname=deezloader-remix-bin
_pkgname=deezloader-rmx
pkgver=4.3.0
pkgrel=1
pkgdesc="An improved version of Deezloader based on the Reborn branch.
        Download songs, playlists and albums directly from Deezer's servers."
arch=('x86_64' 'i686')
url="https://notabug.org/RemixDevs/DeezloaderRemix"
license=('GPL3')
depends=('nss' 'libxss' 'libxtst' 'libappindicator-gtk3' 'libnotify')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}" "${pkgname%-bin}-git")
source_x86_64=("Deezloader_Remix_${pkgver}-x86_64.appimage::https://www.dropbox.com/s/nmznos9sxg72nlf/Deezloader_Remix_${pkgver}-x86_64.appimage?dl=0")
source_i686=("Deezloader_Remix_${pkgver}-i686.appimage::https://www.dropbox.com/s/glzljv2t8eugz7g/Deezloader_Remix_${pkgver}-i386.appimage?dl=0")
md5sums_x86_64=('8ab7860b602e311ff9571a5b371f193f')
md5sums_i686=('88a041670e8760f1d68daea13889e341')

prepare() {
	chmod +x "Deezloader_Remix_${pkgver}-$CARCH.appimage"
	./"Deezloader_Remix_${pkgver}-$CARCH.appimage" --appimage-extract

	sed -i 's|Exec=AppRun|Exec=deezloader-remix|g' "squashfs-root/$_pkgname.desktop"
	sed -i 's|Icon=deezloader-rmx|Icon=deezloader-remix|g' "squashfs-root/$_pkgname.desktop"
}

package() {
	find squashfs-root/{locales,resources,usr/share/icons}/ -type d -exec chmod 755 {} +

	install -d "$pkgdir/opt/${pkgname%-bin}"
	cp -r squashfs-root/* "$pkgdir/opt/${pkgname%-bin}"
	rm -rf "$pkgdir/opt/${pkgname%-bin}/usr"
	rm "$pkgdir/opt/${pkgname%-bin}/$_pkgname.desktop"

	install -dm755 "$pkgdir/usr/bin"
    ln -s "/opt/${pkgname%-bin}/$_pkgname" "$pkgdir/usr/bin/${pkgname%-bin}"

    install -Dm644 "squashfs-root/$_pkgname.desktop" \
    	"$pkgdir/usr/share/applications/${pkgname%-bin}.desktop"

	for icon_size in 16 24 32 48 64 96 128 256 512 1024; do
		icons_dir=/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
		install -d $pkgdir/$icons_dir
		install -m644 squashfs-root/$icons_dir/$_pkgname.png \
			$pkgdir$icons_dir/${pkgname%-bin}.png
	done
}
