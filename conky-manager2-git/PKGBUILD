# https://aur.archlinux.org/packages/conky-manager2-git
pkgname=conky-manager2-git
pkgver=2.4.r45.gd0d6cb2
pkgrel=1
pkgdesc="GUI for managing Conky widgets. Support added for latest conky config version"
arch=('x86_64')
url="https://github.com/zcot/conky-manager2"
license=('GPL3')
depends=('cairo' 'conky' 'desktop-file-utils' 'gtk3' 'imagemagick' 'json-glib' 'libgee'
         'p7zip' 'rsync')
makedepends=('git' 'vala')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/zcot/conky-manager2.git'
        "makefile-roundup.patch")
sha256sums=('SKIP'
            '4aeb8af3248a18980545cdbce580f8ec14eeb6a3aa20f1d7accbc6e4b7c8e7c8')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	patch -Np0 < "../makefile-roundup.patch"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" install
}
