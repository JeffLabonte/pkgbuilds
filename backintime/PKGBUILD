# https://gitlab.manjaro.org/packages/community/backintime
# https://aur.archlinux.org/packages/backintime
pkgname=('backintime' 'backintime-cli')
pkgbase=backintime
pkgver=1.2.1
pkgrel=1
arch=('any')
url="https://github.com/bit-team/backintime"
license=('GPL')
makedepends=('openssh' 'python-dbus' 'python-keyring' 'rsync' 'systemd' 'xorg-xdpyinfo')
#checkdepends=('gocryptfs')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('b94ecdc6731236d6b640270dbd34ef59c8113c34ac7831de3e8f79eea465786a')
validpgpkeys=('3E70692EE3DB8BDDA5991C90615F366D944B4826')
              # Germar Reitze <germar.reitze@gmail.com>

build() {
	cd "$pkgbase-$pkgver/common"
	./configure --python --no-fuse-group
	make

	cd .. && cd qt
	./configure --python
	make
}

check() {
	cd "$pkgbase-$pkgver/common"
	make test
}

package_backintime-cli() {
	pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault. CLI version."
	depends=('cron' 'fuse2' 'openssh' 'python-dbus' 'python-keyring' 'rsync')
	optdepends=('encfs: encrypted filesystems'
				'sshfs: remote filesystems')

	cd "$pkgbase-$pkgver/common"
	make DESTDIR="$pkgdir/" install
}

package_backintime() {
	pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault. Qt5 GUI version."
	depends=('backintime-cli' 'libnotify' 'polkit' 'python-dbus' 'python-pyqt5' 'xorg-xdpyinfo')
	optdepends=('python-secretstorage: store passwords'
	            'kwallet: store passwords'
	            'gnome-keyring: store passwords'
				'kompare: diff tool'
				'meld: diff tool')

	cd "$pkgbase-$pkgver/qt"
	make DESTDIR="$pkgdir/" install
}
