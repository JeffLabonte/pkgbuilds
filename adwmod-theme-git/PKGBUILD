pkgname=adwmod-theme-git
_pkgname=adwaita-extended
pkgver=r138.15594c1
pkgrel=1
pkgdesc="A modified default GNOME/GTK theme with some extras/enhancements/additions."
arch=('any')
url="https://gitlab.com/hrdwrrsk/AdwMod-theme"
license=('GPL3')
makedepends=('git' 'meson' 'sassc')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=('adwaita-extended-git')
source=("${pkgname%-git}::git+https://gitlab.com/hrdwrrsk/AdwMod-theme.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	arch-meson "${pkgname%-git}" build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
