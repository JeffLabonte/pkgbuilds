pkgname=planner-git
pkgver=r274.57e1ad5
pkgrel=1
pkgdesc="Personal task manager designed for elementary OS"
arch=('x86_64')
url="https://planner-todo.web.app"
license=('GPL3')
depends=('gtk3' 'libsoup' 'granite' 'webkit2gtk' 'evolution-data-server' )
makedepends=('git' 'meson' 'vala' 'cmake' 'appstream')
optdepends=()
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/alainm23/planner.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	sed -i 's/libecal-1.2/libecal-2.0/g' meson.build
}

build() {
	cd "$srcdir/${pkgname%-git}"
	arch-meson build
	ninja -C build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	DESTDIR="$pkgdir" ninja -C build install
}
