pkgname=fwupd
pkgver=1.2.8
pkgrel=1
pkgdesc="A simple daemon to allow session software to update firmware"
arch=('i686' 'x86_64')
url="https://github.com/hughsie/fwupd"
license=('GPL2')
depends=('libgusb' 'modemmanager')
makedepends=('meson' 'valgrind' 'gobject-introspection' 'gtk-doc' 'python-pillow' 'git'
			'python-cairo' 'noto-fonts' 'noto-fonts-cjk' 'python-gobject' 'vala'
			'libsoup' 'polkit' 'gcab')
optdepends=('tpm2-abrmd: TPM2 support'
            'tpm2-tools: TPM2 support')
#checkdepends=('umockdev')
backup=('etc/fwupd/daemon.conf' 'etc/fwupd/uefi.conf')
source=("$pkgname.$pkgver.tar.gz::https://github.com/hughsie/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('61b39918ce4c948b055ee090014801bc6777b953d2ed94159b9cbfd5add2f385')

build() {
	cd "$pkgname-$pkgver"
	if [ -n "$CI" ]; then
		export CI="--werror"
	fi
	arch-meson -D b_lto=false $CI ../build
	ninja -v -C ../build
}

#check() {
#	cd "$pkgname-$pkgver"
#	ninja -C ../build test
#}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C ../build install
	
	# Fixup mode to match polkit
	install -d -o root -g 102 -m 750 "$pkgdir"/usr/share/polkit-1/rules.d
	
	# Move D-BUS policy
	mv "$pkgdir"/{etc,usr/share}/dbus-1/system.d
	rmdir "$pkgdir"/etc/dbus-1
	
	# Remove the tests
	rm -r "$pkgdir"/usr/share/installed-tests/
}
