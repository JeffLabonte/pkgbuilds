# https://aur.archlinux.org/packages/linux-wifi-hotspot
pkgname=linux-wifi-hotspot
pkgver=2.0.0
pkgrel=1
pkgdesc='Create virtual wifi hotspot using same wifi card which is connected to an AP'
arch=('x86_64')
url="https://github.com/lakinduakash/linux-wifi-hotspot"
license=('BSD')
depends=('gtk3' 'bash' 'hostapd' 'iproute2' 'iw' 'dnsmasq' 'iptables' 'util-linux'
         'procps-ng' 'which')
makedepends=('cmake')
optdepends=('haveged: For random MAC generation')
provides=('create_ap' 'wihotspot')
conflicts=('create_ap' 'wihotspot')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('c364b3ac184f7939a8ed2d545e958791cfabc94ea9d5dcc077ca8625ef9b96c0')

prepare() {
	cd "$pkgname-$pkgver"

	# Cannot use visudo in fakeroot environment, move to install file
	sed -i '15d' src/scripts/Makefile

	sed -i "s|running_info|h_running_info|g" src/ui/h_prop.c
	sed -i "s|running_info|h_running_info|g" src/ui/h_prop.h
	sed -i "566s|get_running_info|get_h_running_info|" src/ui/ui.c

	sed -i 's|Icon=/usr/share/wihotspot/hotspot.png|Icon=wihotspot.png|g' \
		src/desktop/wifihotspot.desktop
}

build() {
	cd "$pkgname-$pkgver"
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 src/desktop/hotspot.png \
		"$pkgdir/usr/share/pixmaps/wihotspot.png"
	install -Dm644 src/desktop/wifihotspot.desktop \
		"$pkgdir/usr/share/applications/wihotspot.desktop"
}
