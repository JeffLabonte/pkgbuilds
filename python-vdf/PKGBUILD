pkgname=('python-vdf' 'python2-vdf')
pkgbase=('python-vdf')
_pkgname=vdf
pkgver=3.1
pkgrel=1
pkgdesc="Library for working with Valve's VDF text format"
arch=('any')
url="https://github.com/ValvePython/vdf"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=($_pkgname-$pkgver.tar.gz::"https://github.com/ValvePython/$_pkgname/archive/v$pkgver.tar.gz"
		'https://raw.githubusercontent.com/ValvePython/vdf/master/LICENSE')
sha256sums=('6f7e87e0d1921a170032720b3e1e3ce260170faada2ee606539cb4ab022f52a3'
            '87c46dbdd422e99400f428873b6a23fd5526d3362926ba17351db7c45771bdea')

package_python-vdf() {
	depends=('python')
	cd "$_pkgname-$pkgver"
	python setup.py install --optimize=1 --root="$pkgdir/"
    install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-vdf() {
	depends=('python2')
	cd "$_pkgname-$pkgver"
	python2 setup.py install --optimize=1 --root="$pkgdir/"
    install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
