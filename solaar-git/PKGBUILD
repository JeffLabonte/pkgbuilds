pkgname=solaar-git
pkgver=0.9.2.r273.gf455723
pkgrel=1
pkgdesc="Device manager for Logitech's Unifying receiver peripherals"
url="https://github.com/pwr-Solaar/Solaar"
license=('GPL2')
arch=('any')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
depends=('gtk3' 'libnotify' 'python-dbus' 'python-gobject' 'python-pyudev')
source=("${pkgname%-git}::git+https://github.com/pwr/Solaar.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  python setup.py build
}

package() {
  cd "$srcdir/${pkgname%-git}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="$pkgdir/"
  # udev
  install -vDm 644 rules.d/42-logitech-unify-permissions.rules \
    "$pkgdir/usr/lib/udev/rules.d/42-logitech-unify-permissions.rules"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "$pkgdir/usr/share/doc/${pkgname%-git}/"
    
  # Fix typo #534: https://github.com/pwr-Solaar/Solaar/pull/534/commits/5e35482d51ff1f01c7a34c611525f720eed52a35
  sed -i 's/slef.product_id/self.product_id/g' $pkgdir/usr/lib/python3.7/site-packages/logitech_receiver/receiver.py
}
