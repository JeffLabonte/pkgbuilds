# http://aur.archlinux.org/packages/puddletag-git
pkgname=puddletag-git
pkgver=1.2.0.r150.g66636fc
pkgrel=1
pkgdesc="An audio tag editor for GNU/Linux."
arch=('i686' 'x86_64')
url="http://docs.puddletag.net"
license=('GPL')
depends=('python-pyqt5' 'python-pyparsing' 'python-mutagen' 'python-configobj'
         'python-pillow' 'qt5-svg')
makedepends=('git' 'python-setuptools')
optdepends=('quodlibet: import / edit a QuodLibet library'
            'chromaprint: for AcoustID support')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" "${pkgname%-qt5-git}")
replaces=("${pkgname%-qt5-git}")
source=('git+https://github.com/puddletag/puddletag.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}/source"
	python setup.py config
}

package() {
	cd "$srcdir/${pkgname%-git}/source"
	python setup.py install --root="$pkgdir" --optimize=1
}
