# https://www.archlinux.org/packages/community/any/nicotine+
pkgname=nicotine+
pkgver=2.1.2
pkgrel=1
pkgdesc="Soulseek music-sharing client, written in Python"
arch=('any')
url="https://github.com/Nicotine-Plus/nicotine-plus"
license=('GPL')
depends=('python-miniupnpc' 'python-pytaglib' 'python-gobject' 'gtk3' 'libnotify')
optdepends=('gspell: for spell checking in chat'
            'nuspell: for spell checking in chat'
            'libappindicator-gtk3: for tray icon')
source=("$url/archive/$pkgver/nicotine-$pkgver.tar.gz")
sha256sums=('3ed18ade97183c632836eb8e304a515fc19a35babb46cc6e6747bcfd8205dcdf')

build() {
	cd nicotine-plus-$pkgver
	python setup.py build
}

package() {
	cd nicotine-plus-$pkgver
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
