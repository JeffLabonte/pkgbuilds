# https://www.archlinux.org/packages/community/any/nicotine+
pkgname=nicotine+
pkgver=2.2.2
pkgrel=1
pkgdesc="Soulseek music-sharing client, written in Python"
arch=('any')
url="https://github.com/Nicotine-Plus/nicotine-plus"
license=('GPL')
depends=('python-gobject' 'gtk3')
optdepends=('gspell: for spell checking in chat'
            'libappindicator-gtk3: for tray icon')
source=("$url/archive/$pkgver/nicotine-$pkgver.tar.gz")
sha256sums=('a2734f11beb7ec4e32d1c8270b7328721b841830b22aed2db3c4acf2b66791bb')

build() {
	cd nicotine-plus-$pkgver
	python setup.py build
}

package() {
	cd nicotine-plus-$pkgver
	export PYTHONHASHSEED=0
	python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
