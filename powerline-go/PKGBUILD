pkgname=powerline-go
pkgver=1.13.0
pkgrel=1
pkgdesc="A beautiful, useful and fast prompt for your shell"
arch=('x86_64')
url="https://github.com/justjanne/powerline-go"
license=('GPL3')
makedepends=('go-pie' 'dep')
optdepends=('powerline-fonts')
source=("$pkgname-$pkgver.tar.gz::https://github.com/justjanne/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('0c0d8a2aca578391edc0120f6cbb61f9ef5571190c07a978932348b0489d00ea')

prepare() {
	mkdir -p gopath/src/github.com/justjanne
	ln -rTsf "$pkgname-$pkgver" "gopath/src/github.com/justjanne/$pkgname"
	export GOPATH="$srcdir"/gopath
	
	cd "gopath/src/github.com/justjanne/$pkgname"
	dep init
	dep ensure
}

build() {
	cd "$pkgname-$pkgver"
	go build \
	-gcflags "all=-trimpath=${PWD}" \
	-asmflags "all=-trimpath=${PWD}" \
	-ldflags "-extldflags=-zrelro" \
	-ldflags "-extldflags=-znow" \
	.
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
}
