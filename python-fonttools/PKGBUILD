pkgname=python-fonttools
_name=${pkgname#python-}
pkgver=4.2.3
pkgrel=1
pkgdesc="A library to manipulate font files from Python"
url="https://github.com/fonttools/fonttools"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
optdepends=('python-lxml: faster backend for XML files reading/writing'
            'python-fs: to read/write UFO source files'
	        'python-brotli: to compress/decompress WOFF 2.0 web fonts'
	        'python-zopfli: faster backend fom WOFF 1.0 web fonts compression'
	        'python-scipy: for finding wrong contour/component order between different masters'
	        'python-matplotlib: for visualizing DesignSpaceDocument and resulting VariationModel'
	        'python-sympy: for symbolic font statistics analysis'
	        'python-pyqt5: for drawing glyphs with QPainterPath'
	        'python-reportlab: to drawing glyphs as PNG images')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.zip"
        "https://raw.githubusercontent.com/fonttools/fonttools/master/LICENSE")
sha256sums=('da069660a5074a538729aa908b25ceab5fb8bd39e0bf6c0c21b5e6405672c8c5'
            '6787208f83f659ccbc2223b2fde952ffa6f7e8aca62f1a8a2bf5bc51bb1b2383')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 "$srcdir/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
