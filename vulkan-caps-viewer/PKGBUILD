pkgname=vulkan-caps-viewer
pkgver=2.03
pkgrel=1
pkgdesc='Vulkan Hardware Capability Viewer'
arch=('x86_64')
url="http://vulkan.gpuinfo.org"
license=('GPL2')
depends=('vulkan-icd-loader' 'qt5-base' 'qt5-x11extras')
makedepends=('git')
source=("git+https://github.com/SaschaWillems/VulkanCapsViewer.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
	cd "$srcdir/VulkanCapsViewer"
	git submodule update --init --recursive

	# The last commit of 2.02 explicitely breaks the build;
	# probably wasn't intended to be pushed out
	sed 's#"/Vulkan-Headers/include"#"./Vulkan-Headers/include"#' -i vulkanCapsViewer.pro
}

build() {
	cd "$srcdir/VulkanCapsViewer"
	qmake

	make PREFIX=/usr
}

package() {
	cd "$srcdir/VulkanCapsViewer"
	make INSTALL_ROOT="$pkgdir/" install

	# There's a bug preventing this from being installed automatically
	install -Dm644 gfx/android_icon_256.png \
		"$pkgdir"/usr/share/icons/hicolor/256x256/apps/vulkanCapsViewer.png
}
