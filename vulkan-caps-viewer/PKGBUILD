pkgname=vulkan-caps-viewer
pkgver=2.02
pkgrel=1
pkgdesc='Vulkan Hardware Capability Viewer'
arch=('x86_64')
url="http://vulkan.gpuinfo.org"
license=('GPL2')
depends=('vulkan-icd-loader' 'qt5-base' 'qt5-x11extras')
makedepends=('git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/SaschaWillems/VulkanCapsViewer/archive/$pkgver.tar.gz"
        'git+https://github.com/KhronosGroup/Vulkan-Headers.git')
sha1sums=('bd4edf5510968bd5c9c2bce0deb4c048b7ddf966'
          'SKIP')

prepare() {
  cd "$srcdir/Vulkan-Headers"
  git submodule init
  mv "$srcdir/Vulkan-Headers/"* "$srcdir/VulkanCapsViewer-$pkgver/Vulkan-Headers"

  # HACK: the last commit of 2.02 explicitely breaks the build; probably wasn't intended to be pushed out
  sed 's#"/Vulkan-Headers/include"#"./Vulkan-Headers/include"#' -i \
    "$srcdir/VulkanCapsViewer-$pkgver/vulkanCapsViewer.pro"
}

build() {
  if [ -d build ]
  then
    msg2 "Build dir already exist; performing an incremental build"
    msg2 "If you want to perform a clean build, please delete $(realpath build)"
    return
  fi
  
  mkdir build
  cd build
  export PREFIX="$pkgdir"
  qmake \
    QMAKE_CFLAGS="$CFLAGS" \
    QMAKE_CXXFLAGS="$CXXFLAGS" \
    QMAKE_LFLAGS="$LDFLAGS" \
    PREFIX=/usr \
    ../VulkanCapsViewer-$pkgver
    
  make
}

package() {
  make -C build INSTALL_ROOT="$pkgdir/" install

  # There's a bug preventing this from being installed automatically
  install -Dm644 VulkanCapsViewer-$pkgver/gfx/android_icon_256.png \
    "$pkgdir"/usr/share/icons/hicolor/256x256/apps/vulkanCapsViewer.png
}
