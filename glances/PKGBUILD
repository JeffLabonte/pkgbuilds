# https://www.archlinux.org/packages/community/any/glances
pkgname=glances
pkgver=3.1.4.1
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
makedepends=('git' 'python-setuptools')
depends=('python-psutil' 'python-setuptools' 'python-future')
optdepends=('hddtemp: HDD temperature monitoring support'
            'lm_sensors: '
            'python-bottle: for Web server mode'
            'python-cassandra-driver: for the Cassandra export module'
            'python-couchdb: for the CouchDB export module'
            'python-docker: for the Docker monitoring support'
            'python-elasticsearch: for the Elastic Search export module'
            'python-influxdb: for the InfluxDB export module'
            'python-kafka: for the Kafka export module'
            'python-netifaces: for the IP plugin'
            'python-py3nvml: '
            'python-paho-mqtt: '
            'python-pika: for the RabbitMQ/ActiveMQ export module'
            'python-prometheus_client: for the Prometheus export module'
            'python-py-cpuinfo: for the Quicklook CPU info module'
            'python-pygal: for the graph export module'
            'python-pysnmp: for SNMP support'
            'python-pystache: for the action script feature'
            'python-pyzmq: for the ZeroMQ export module'
            'python-requests: for the Ports, Cloud plugins and RESTful export module'
            'python-statsd: for the StatsD export module'
            'python-wifi: for the wifi plugin'
            'python-zeroconf: for the autodiscover mode'
            'wireless_tools: for the Wifi plugin')

            # Missing from repos / AUR

            #'python-bernhard: for the Riemann export module'
            #'python-potsdb: for the OpenTSDB export module'
            #'python-pymdstat: for RAID support'
            #'python-pysmart.smartx: for HDD Smart support'
            #'python-sparklines: '
changelog=NEWS.rst
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname.service")
sha256sums=('0347a0b949451fd0022c0f22e54092fe526120a776af1f2bde1ea7ba61d6b792'
            '4cdd6955ec14146a9975bd5a6fe0fe15081b0d83fc8a062e4a68b8e432317308')

build() {
	cd "$pkgname-$pkgver"
	python setup.py build
}

package() {
	cd "$pkgname-$pkgver"
	python setup.py install --root="$pkgdir/" --skip-build --optimize=1

	install -Dm644 "$srcdir/$pkgname.service" -t "$pkgdir/usr/lib/systemd/system"
}
