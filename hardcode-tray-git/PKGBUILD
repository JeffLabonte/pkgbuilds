# https://aur.archlinux.org/packages/hardcode-tray-git
pkgname=hardcode-tray-git
pkgver=4.3.r122.gc705bff
pkgrel=1
pkgdesc="Fixes hardcoded tray icons"
arch=('i686' 'x86_64')
url="https://github.com/bil-elmoussaoui/Hardcode-Tray"
license=('GPL3')
depends=('python-gobject' 'python-cairosvg' 'librsvg' 'gtk3')
makedepends=('git' 'meson')
optdepends=('inkscape: convert svg to png'
            'imagemagick: convert svg to png')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/bil-elmoussaoui/Hardcode-Tray.git"
        "90-${pkgname%-git}.hook"
        "hook.py")
sha256sums=('SKIP'
            'c16ba6a82d4fe523cb91bf472b44296db82ff301866e3bce18bb4149d0aae5ea'
            '64a9907f74c5c04fb9f32b2b0684a3ce040d3bc2baacabb3ca1e914d70f24c81')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	arch-meson "${pkgname%-git}" build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	# Add pacman hook
	# https://github.com/bilelmoussaoui/Hardcode-Tray/issues/454
	install -Dm644 "$srcdir/90-${pkgname%-git}.hook" -t "$pkgdir/etc/pacman.d/hooks"
	install -Dm644 "$srcdir/hook.py" -t "$pkgdir/usr/share/${pkgname%-git}"
}
