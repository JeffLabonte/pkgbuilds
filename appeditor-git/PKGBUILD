pkgname=appeditor-git
pkgver=1.1.0.r11.caacf30
pkgrel=1
pkgdesc="AppEditor allows you to edit application entries in the application menu."
arch=('i686' 'x86_64')
url="https://github.com/donadigo/appeditor"
license=('GPL3')
depends=('granite')
makedepends=('git' 'meson' 'vala')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git://github.com/donadigo/appeditor.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	sed -i 's/unowned string _path = info.get_string/string _path = info.get_string/g' \
		src/DesktopApp.vala
}

build() {
	cd "$srcdir/${pkgname%-git}"
	meson build
	cd build
	meson configure -Dprefix=/usr
	ninja
}

package() {
	cd "$srcdir/${pkgname%-git}/build"
	DESTDIR="$pkgdir/" ninja install

	ln -s /usr/bin/com.github.donadigo.appeditor "$pkgdir/usr/bin/${pkgname%-git}"
}
