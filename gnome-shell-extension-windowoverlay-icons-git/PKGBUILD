pkgname=gnome-shell-extension-windowoverlay-icons-git
pkgver=36.r1.g55031b5
pkgrel=1
pkgdesc="Adds application icons to the windows overview (in Activities menu)"
arch=('any')
url="https://github.com/sustmi/gnome-shell-extension-windowoverlay-icons"
license=('GPL3')
depends=('gnome-shell')
makedepends=('git' 'zip')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/sustmi/gnome-shell-extension-windowoverlay-icons.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make schemas
	make locales
	make zip
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/usr/share/gnome-shell/extensions/windowoverlay-icons@sustmidown.centrum.cz"
	bsdtar xvf windowoverlay-icons.zip -C \
		"$pkgdir/usr/share/gnome-shell/extensions/windowoverlay-icons@sustmidown.centrum.cz"
}
