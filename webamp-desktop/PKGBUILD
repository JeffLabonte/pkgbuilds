pkgname=webamp-desktop
pkgver=0.3.0
pkgrel=1
pkgdesc="Desktop version of Winamp 2.9 reimplementation in HTML5 and JS"
arch=('i686' 'x86_64')
url="https://desktop.webamp.org"
license=('MIT')
depends=('gconf' 'libnotify' 'libappindicator-gtk2' 'libxtst' 'nss' 'libxss')
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/durasj/webamp-desktop/archive/v$pkgver.tar.gz")
sha256sums=('7ee397e49b6707c6922c9e61078198aa65afcbb867b020c38ae295a24b1d14c6')

prepare () {
	cd "$pkgname-$pkgver"
	export JOBS=max
	yarn --ignore-scripts
}

build() {
	cd "$pkgname-$pkgver"
	export JOBS=max
	yarn dist
}

package() {
	cd "$pkgname-$pkgver"
	install -d $pkgdir/{opt/$pkgname,usr/bin,usr/share/applications}
	cp -r artifacts/linux-unpacked/* $pkgdir/opt/$pkgname
	ln -s $pkgdir/opt/$pkgname/$pkgname $pkgdir/usr/bin/$pkgname
	install -Dm644 res/icon.png $pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

	cat <<EOF >$pkgdir/usr/share/applications/$pkgname.desktop
[Desktop Entry]
Name=Webamp desktop
Comment=Desktop version of Winamp 2.9 reimplementation in HTML5 and JS
Exec="/opt/webamp-desktop/webamp-desktop" %U
Terminal=false
Type=Application
Icon=webamp-desktop
StartupWMClass=Webamp desktop
Categories=AudioVideo;
EOF
}
