# http://aur.archlinux.org/packages/scrcpy
pkgname=scrcpy
pkgver=1.13
pkgrel=1
pkgdesc='Display and control your Android device'
arch=('i686' 'x86_64')
url='https://github.com/Genymobile/scrcpy'
license=('Apache')
depends=('adb' 'ffmpeg' 'sdl2')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
noextract=("$pkgname-server"*)
sha256sums=('6f65a22b495240358a56f84098edc48e9e396eab20b6130538088d96eb7e36f0'
            '5fee64ca1ccdc2f38550f31f5353c66de3de30c2e929a964e30fa2d005d5f885')

build() {
    cd "$pkgname-$pkgver"
    arch-meson \
        -Dprebuilt_server="../$pkgname-server-v$pkgver" \
        build
    ninja -C build
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
