# http://aur.archlinux.org/packages/scrcpy
pkgname=scrcpy
pkgver=1.15.1
pkgrel=1
pkgdesc='Display and control your Android device'
arch=('i686' 'x86_64')
url='https://github.com/Genymobile/scrcpy'
license=('Apache')
depends=('adb' 'ffmpeg' 'sdl2')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
noextract=("$pkgname-server"*)
sha256sums=('1d66dccf14f77e085853453d91a06fad40d1ab5cf2997f00079502edeac9d575'
            'fe06bd6a30da8c89860bf5e16eecce2b5054d4644c84289670ce00ca5d1637c3')

build() {
    arch-meson \
        -Dprebuilt_server="../$pkgname-server-v$pkgver" \
        "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	cd "$pkgname-$pkgver"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
