# http://aur.archlinux.org/packages/scrcpy
pkgname=scrcpy
pkgver=1.14
pkgrel=1
pkgdesc='Display and control your Android device'
arch=('i686' 'x86_64')
url='https://github.com/Genymobile/scrcpy'
license=('Apache')
depends=('adb' 'ffmpeg' 'sdl2')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
noextract=("$pkgname-server"*)
sha256sums=('45353b6e6cf1efbc8aadf2a51103242460562f6a55c6772037f492716b3fca07'
            '1d1b18a2b80e956771fd63b99b414d2d028713a8f12ddfa5a369709ad4295620')

build() {
    cd "$pkgname-$pkgver"
    arch-meson \
        -Dprebuilt_server="../$pkgname-server-v$pkgver" \
        build
    ninja -C build
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
