pkgname=cage-git
pkgver=r62.443d955
pkgrel=1
pkgdesc="A Wayland kiosk"
arch=('any')
url="https://www.hjdskes.nl/projects/cage"
license=('MIT')
depends=('libinput' 'libxkbcommon' 'wayland' 'wayland-protocols' 'wlroots' 'xorg-server-xwayland')
makedepends=('git' 'meson')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install=cage.install
source=("git+https://github.com/Hjdskes/cage.git"
		"cage@.service::https://raw.githubusercontent.com/Hjdskes/buildroot/hjdskes/board/hjdskes/rpi3/rootfs_overlay/etc/systemd/system/cage%40.service"
		"cage.pam::https://raw.githubusercontent.com/Hjdskes/buildroot/hjdskes/board/hjdskes/rpi3/rootfs_overlay/etc/pam.d/cage"
		"https://raw.githubusercontent.com/Hjdskes/cage/master/LICENSE")
sha256sums=('SKIP'
            '87fe1421d31c8ee6ecd9c34996e29b33dd0ca2bfb5955a7783504ed0efbe25c3'
            'd6d3f1f951db72c48bdec8437a306b514d5ad5a49694e09e422d15dc33f12d9d'
            '1fd28695090b961ed2846627365489228d84f784bff60a90e933febcedaeb65d')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	meson build --prefix /usr -Dxwayland=true
	ninja -C build
}

package() {
	cd "$srcdir/${pkgname%-git}"
	DESTDIR="$pkgdir/" ninja -C build install
	install -Dm644 "$srcdir/cage@.service" "$pkgdir/etc/system.d/system/cage@.service"
	install -Dm644 "$srcdir/cage.pam" "$pkgdir/etc/pam.d/cage"
	install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
