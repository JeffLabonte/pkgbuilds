pkgname=qogir-gtk-theme-git
pkgver=2020.02.26.r2.gb00c1ab
pkgrel=1
pkgdesc='A flat design theme for GTK 3, GTK 2 and Gnome-Shell'
arch=('any')
url='https://github.com/vinceliuice/Qogir-theme'
license=('GPL3')
depends=('gtk3' 'gtk-engine-murrine' 'gtk-engines')
makedepends=('git')
optdepends=('kvantum-theme-qogir: Matching Kvantum theme'
            'qogir-icon-theme: Matching icon theme'
            'tela-icon-theme: Recommended icon theme')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!strip')
source=("${pkgname%-git}::git+https://github.com/vinceliuice/Qogir-theme.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/usr/share/themes"
	./install.sh -d "$pkgdir/usr/share/themes"
}
