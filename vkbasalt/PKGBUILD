# https://aur.archlinux.org/packages/vkbasalt
pkgname=vkbasalt
pkgver=0.3.2.4
pkgrel=1
pkgdesc="A Vulkan post processing layer to enhance the visual graphics of games"
arch=('x86_64')
url="https://github.com/DadSchoorse/vkBasalt"
license=('zlib')
depends=('gcc-libs' 'libx11')
makedepends=('meson' 'glslang' 'vulkan-headers' 'spirv-headers')
optdepends=('reshade-shaders-git')
backup=('etc/vkBasalt/vkBasalt.conf')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('f45d513d9f212e9a1e1ec49448e72c5f2510d62303c38fdb5015b9a3afd42b26')

prepare() {
	cd vkBasalt-$pkgver
	sed -i 's|/path/to/reshade-shaders/Textures|/usr/share/reshade/textures|g' \
		config/vkBasalt.conf
	sed -i 's|/path/to/reshade-shaders/Shaders|/usr/share/reshade/shaders|g' \
		config/vkBasalt.conf
}

build() {
	arch-meson vkBasalt-$pkgver build \
		-Dappend_libdir_vkbasalt=true
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	cd vkBasalt-$pkgver
	install -Dm644 config/vkBasalt.conf -t "$pkgdir/etc/vkBasalt"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
