# https://aur.archlinux.org/packages/vkbasalt
pkgname=vkbasalt
pkgver=0.3.2.3
pkgrel=4
pkgdesc="A Vulkan post processing layer to enhance the visual graphics of games"
arch=('x86_64')
url="https://github.com/DadSchoorse/vkBasalt"
license=('zlib')
depends=('gcc-libs' 'libx11')
makedepends=('meson' 'glslang')
optdepends=('reshade-shaders-git')
backup=(etc/vkBasalt/vkBasalt.conf)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('8cc65a69a20ac16802a8ff46629efa6540dee22daeef9db4ad0a22b63d2a4368')

prepare() {
	cd vkBasalt-$pkgver
	sed -i 's|/path/to/reshade-shaders/Textures|/usr/share/reshade/textures|g' \
		config/vkBasalt.conf
	sed -i 's|/path/to/reshade-shaders/Shaders|/usr/share/reshade/shaders|g' \
		config/vkBasalt.conf
}

build() {
	arch-meson \
		-Dappend_libdir_vkbasalt=true \
		vkBasalt-$pkgver build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build64

	cd vkBasalt-$pkgver
	install -Dm644 config/vkBasalt.conf -t "$pkgdir/etc/vkBasalt"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
