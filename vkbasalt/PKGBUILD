# https://aur.archlinux.org/packages/vkbasalt
pkgname=('vkbasalt' 'lib32-vkbasalt')
pkgbase=vkbasalt
pkgver=0.3.2.2+2+g7cd22a1
pkgrel=1
pkgdesc="A Vulkan post-processing layer"
arch=('x86_64')
url="https://github.com/DadSchoorse/vkBasalt"
license=('zlib')
makedepends=('meson' 'glslang')
install="$pkgname.install"
_commit='7cd22a16120d39e8f7d734dacd5dc8f43a4ffeee'
source=("$pkgname::git+https://github.com/DadSchoorse/vkBasalt.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
	cd "$srcdir/$pkgbase"
	sed -i 's|/path/to/reshade-shaders/Textures|/usr/share/reshade/textures|g' \
		config/vkBasalt.conf
	sed -i 's|/path/to/reshade-shaders/Shaders|/usr/share/reshade/shaders|g' \
		config/vkBasalt.conf
}

build() {
	arch-meson \
		-Dappend_libdir_vkbasalt=true \
		"$srcdir/$pkgbase" build64 --libdir=lib/vkbasalt
	meson compile -C build64

	export ASFLAGS=--32
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
	arch-meson \
		-Dappend_libdir_vkbasalt=true \
		-Dwith_json=false \
		"$srcdir/$pkgbase" build32 --libdir=lib32/vkbasalt
	meson compile -C build32
}

package_vkbasalt() {
	depends=('gcc-libs' 'libx11')
	optdepends=('reshade-shaders-git')

	DESTDIR="$pkgdir" meson install -C build64

	cd "$srcdir/$pkgbase"
	install -Dm644 config/vkBasalt.conf \
		"$pkgdir/usr/share/doc/$pkgname/vkBasalt.conf.example"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-vkbasalt() {
	depends=('lib32-gcc-libs' 'lib32-libx11')

	DESTDIR="$pkgdir" meson install -C build32

	cd "$srcdir/$pkgbase"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
