pkgname=boxtron-git
pkgver=0.5.3.r6.8aa2e4e
pkgrel=1
pkgdesc="Steam Play compatibility tool to run DOS games using native Linux DOSBox"
arch=('x86_64')
url="https://luxtorpeda.gitlab.io"
license=('GPL2')
depends=('python' 'dosbox' 'inotify-tools')
makedepends=('git')
#makedepends=('git' 'python-coverage' 'python-pycodestyle' 'python-pyenchant' 'python-pylint'
#             'shellcheck' 'yapf' 'hunspell-en_GB')
optdepends=('timidity++: MIDI support'
            'soundfont-fluid: MIDI support'
            'steam')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/dreamer/boxtron.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"
	sed -i -E '/^tool_(name|dir) +=/s/$/-git/' Makefile
	sed -i -E '/^tool_name_display +=/s/$/ (git)/' Makefile
}

#build() {
	# Only for development: https://github.com/dreamer/boxtron/blob/master/CONTRIBUTING.md

#	cd "$srcdir/${pkgname%-git}"
#	make
#}

check() {
	cd "$srcdir/${pkgname%-git}"
	make test
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir" prefix=/usr install
}
