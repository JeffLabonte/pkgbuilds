pkgname=hardcode-tray
pkgver=4.3
pkgrel=1
pkgdesc="Fixes Hardcoded Tray Icons"
arch=('i686' 'x86_64')
url="https://github.com/bil-elmoussaoui/Hardcode-Tray"
license=('GPL3')
makedepends=('meson')
depends=('python-gobject' 'python-cairosvg' 'librsvg' 'gtk3' 'gobject-introspection')
optdepends=('sni-qt-patched-git: patched qt4 sni plugin to enable icon modification'
            'inkscape: convert svg to png'
            'imagemagick: convert svg to png')
optdepends_x86_64=('lib32-sni-qt-patched-git: 32-bit patched qt4 sni plugin to enable icon modification')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('5f64c5a1083b8cefc07ba3a218bb0258f7846d9c0d04dae6f91c2478e288f0e1')

prepare() {
	cd "Hardcode-Tray-$pkgver"
	# DEPRECATION: Project targetting '>= 0.40.0' but tried to use feature deprecated
	# since '0.48.0': python3 module
	sed -i "s/meson_version: '>= 0.40.0',/meson_version: '>= 0.49',/g" meson.build
}

build() {
	cd "Hardcode-Tray-$pkgver"
	arch-meson builddir
	ninja -C builddir
}

package() {
	cd "Hardcode-Tray-$pkgver"
	DESTDIR="$pkgdir" ninja -C builddir install
}
