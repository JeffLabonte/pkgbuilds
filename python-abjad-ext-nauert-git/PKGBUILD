pkgname=python-abjad-ext-nauert-git
_name=abjad-ext-nauert
pkgver=3.0.0.r32.g62ebba3
pkgrel=1
pkgdesc="Abjad quantization extension, based on Paul Nauert's Q-Grids"
arch=('any')
url="https://github.com/Abjad/abjad-ext-nauert"
license=('MIT')
depends=('python-abjad')
makedepends=('git' 'python-setuptools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/Abjad/abjad-ext-nauert.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_name"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$_name"
	python setup.py build
}

package() {
	cd "$srcdir/$_name"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
