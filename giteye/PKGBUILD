pkgname=giteye
pkgver=2.2.0
pkgrel=1
pkgdesc="A desktop for Git that works with TeamForge, CloudForge and other Git services."
arch=('i686' 'x86_64')
url="https://www.collab.net/products/giteye"
license=('custom')
depends=('git' 'java-runtime>=8' 'python')
options=('!strip')
source_i686=("https://www.collab.net/sites/default/files/downloads/GitEye-$pkgver-linux.x86.zip")
source_x86_64=("https://www.collab.net/sites/default/files/downloads/GitEye-$pkgver-linux.x86_64.zip")
source=("LICENSE"
        "$pkgname.desktop")
md5sums=('78ba2a04c6b766a778681705d40abbb4'
         'aad2bccbc197010b659e7a38e9c1c8f7')
md5sums_i686=('011f3e144f1e7abecca266fbfdfe4815')
md5sums_x86_64=('117d6e1e905aea0ec91ce18e155e87a1')
noextract=(*.zip) # extract nothing


package() {
    cd "$srcdir"

    msg2 "Extracting GitEye from archive..."
    install -Ddm755 "$pkgdir/opt/$pkgname/"
	install -Ddm755 "$pkgdir/usr/bin/"

    bsdtar -xf "GitEye-$pkgver-linux.$CARCH.zip" -C "$pkgdir/opt/$pkgname/"

	msg2 "Adding GitEye executable to default path..."
	ln -s "/opt/$pkgname/GitEye" "$pkgdir/usr/bin/$pkgname"

    msg2 "Installing LICENSE..."
	if [[ "$CARCH" = "i686" ]]; then
		sed -i 's/64-bit/32-bit/' LICENSE
	fi
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    msg2 "Installing desktop file and icon..."
    install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "$pkgdir/opt/$pkgname/icon.xpm" "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
}
