pkgname=cef-minimal
pkgver=76.1.13
_pkgver=76.1.13+gf19c584+chromium-76.0.3809.132
_url_pkgver=76.1.13%2Bgf19c584%2Bchromium-76.0.3809.132
pkgrel=1
pkgdesc="Chromium Embedded Framework minimal release"
arch=('x86_64')
url="https://bitbucket.org/chromiumembedded/cef"
license=('BSD')
depends=('nss' 'alsa-lib' 'libxss' 'libxtst' 'libglvnd' 'pango' 'libxcursor' 'dbus' 'libxrandr' 'libxcomposite' 'at-spi2-atk')
makedepends=('cmake')
provides=('cef' 'cef=76.1.13')
conflicts=('cef')
source=("http://opensource.spotify.com/cefbuilds/cef_binary_${_url_pkgver}_linux64_minimal.tar.bz2"{,.sha1})
sha1sums=('014879a4b9696e91605ef2c135598db8ca15e543'
          'SKIP')

prepare() {
	cd "$srcdir/cef_binary_${_pkgver}_linux64_minimal"
	mkdir -p build
}

build() {
	cd "$srcdir/cef_binary_${_pkgver}_linux64_minimal/build"
	cmake -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release ..
	make
}
package() {
	cd "$srcdir/cef_binary_${_pkgver}_linux64_minimal"
	install -d "$pkgdir"/usr/{include/cef,lib/cef}
	cp -r include "$pkgdir/usr/include/cef"
	cp -r Release/* Resources/* "$pkgdir/usr/lib/cef"
	cp -r build/libcef_dll_wrapper/libcef_dll_wrapper.a -t "$pkgdir/usr/lib/cef"

	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/cef"
}
