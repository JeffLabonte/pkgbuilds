# http://aur.archlinux.org/packages/puddletag-qt5-git
pkgname=puddletag-qt5-git
pkgver=1.2.0.r119.g3c5e2dc
pkgrel=1
pkgdesc="An audio tag editor for GNU/Linux."
arch=('i686' 'x86_64')
url="http://docs.puddletag.net"
license=('GPL')
depends=('python-pyqt5' 'python-pyparsing' 'python-mutagen' 'python-configobj'
         'python-pillow' 'qt5-svg')
makedepends=('git' 'python-setuptools')
optdepends=('quodlibet: import / edit a QuodLibet library'
            'chromaprint: for AcoustID support')
provides=("${pkgname%-git}" "${pkgname%-qt5-git}")
conflicts=("${pkgname%-git}" "${pkgname%-qt5-git}")
#source=('git+https://github.com/keithgg/puddletag#branch=pyqt5')
source=('git+https://github.com/sandrotosi/puddletag.git#branch=pyqt5+py3')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-*-*}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/${pkgname%-*-*}/source"
	python setup.py config
}

package() {
	cd "$srcdir/${pkgname%-*-*}/source"
	python setup.py install --root="$pkgdir" --optimize=1
}
