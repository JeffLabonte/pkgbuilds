# https://aur.archlinux.org/packages/python-defcon
pkgname=python-defcon
_name=${pkgname#python-}
pkgver=0.7.2
pkgrel=1
pkgdesc='A set of UFO based objects for use in font editing applications.'
arch=('any')
url="https://github.com/robotools/defcon"
license=('MIT')
depends=('python-fonttools')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
optdepends=('python-fontpens' 'python-lxml')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('1967eaa23dcc8e1cbe3bd130aae4d9ddb72027b62d3e23eeb640065cfbd6d8d1')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
    cd "$_name-$pkgver"
    python setup.py test
}

package() {
    cd "$_name-$pkgver"
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build

    install -Dm644 License.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
