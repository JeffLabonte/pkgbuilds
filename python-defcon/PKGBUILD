# https://aur.archlinux.org/packages/python-defcon
pkgname=python-defcon
_name=${pkgname#python-}
pkgver=0.7.0
pkgrel=1
pkgdesc='A set of UFO based objects for use in font editing applications.'
arch=('any')
url="https://github.com/robotools/defcon"
license=('MIT')
depends=('python-fonttools')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-unicodedata2' 'python-fs' 'python-pytz')
optdepends=('python-fontpens' 'python-lxml')
source=("https://pypi.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.zip")
sha256sums=('61ed498aad56a2276e5d6e320703d53fd3c76e9d68475814fa88349f7477e207')

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
    cd "$_name-$pkgver"
    python setup.py test
}

package() {
    cd "$_name-$pkgver"
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build

    install -Dm644 License.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
