pkgname=chromium-vaapi-bin
pkgver=85.0.4183.83
_pkgrel=1
pkgrel=1
pkgdesc="Chromium with VA-API support to enable hardware acceleration, pre-compiled"
arch=('x86_64')
url="https://www.chromium.org/Home"
license=('BSD')
depends=('gtk3' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'libcups' 'libgcrypt'
         'ttf-liberation' 'systemd' 'dbus' 'libpulse' 'pciutils' 'json-glib'
         'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('pepper-flash: support for Flash content'
            'libpipewire02: WebRTC desktop sharing under Wayland'
            'libva: hardware-accelerated video decode [experimental]'
            'kdialog: needed for file dialogs in KDE'
            'org.freedesktop.secrets: password storage backend on GNOME / Xfce'
            'kwallet: for storing passwords in KWallet on KDE desktops')
provides=("${pkgname%-vaapi-bin}" "${pkgname%-bin}")
conflicts=("${pkgname%-vaapi-bin}" "${pkgname%-bin}")
install="${pkgname%-vaapi-bin}.install"
#source=("https://cdn.repo.archlinuxcn.org/x86_64/${pkgname%-bin}-${pkgver}-${_pkgrel}-${arch}.pkg.tar.zst")
source=("https://download.opensuse.org/repositories/home:/kjliew/Arch/$arch/${pkgname%-bin}-${pkgver}-${_pkgrel}-${arch}.pkg.tar.zst")
sha256sums=('d1e695ffbc72af0679b8667a680a2d5f217d4e2b3ab18d19d11413dd870f5859')

# Keep in sync with AUR/chromium-vaapi:
#
# Possible replacements are listed in build/linux/unbundle/replace_gn_files.py
# Keys are the names in the above script; values are the dependencies in Arch
declare -gA _system_libs=(
	[ffmpeg]=ffmpeg
	[flac]=flac
	[fontconfig]=fontconfig
	[freetype]=freetype2
	[harfbuzz-ng]=harfbuzz
	[icu]=icu
	[libdrm]=
	[libjpeg]=libjpeg
	[libpng]=libpng
	#[libvpx]=libvpx
	[libwebp]=libwebp
	[libxml]=libxml2
	[libxslt]=libxslt
	[opus]=opus
	[re2]=re2
	[snappy]=snappy
	[zlib]=minizip
)
_unwanted_bundled_libs=(
	$(printf "%s\n" ${!_system_libs[@]} | sed 's/^libjpeg$/&_turbo/')
)
depends+=(${_system_libs[@]})

package() {
	cp -ar "$srcdir/usr" "$pkgdir/usr"
	chmod 4755 "$pkgdir/usr/lib/chromium/chrome-sandbox"
}
