pkgname=athenaeum-git
pkgver=v0.3.0.r0.668867a
pkgrel=1
pkgdesc="A libre replacement for Steam"
arch=(any)
url="https://gitlab.com/librebob/athenaeum"
license=('GPL3')
depends=('flatpak' 'python-pyqt5' 'python-peewee' 'python-dateutil' 'qt5-svg' 'qt5-quickcontrols2')
makedepends=('git' 'python-setuptools')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("git+https://gitlab.com/librebob/athenaeum.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

package() {
	cd "$srcdir/${pkgname%-git}"
	python setup.py install --root="$pkgdir" --optimize=1 --prefix=/usr
	for i in 16 32 48 64 96 128 256 512; do
		install -Dm644 "${pkgname%-git}/resources/icons/hicolor/${i}x${i}/com.gitlab.librebob.Athenaeum.png" \
			"$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/com.gitlab.librebob.Athenaeum.png"
	done
	install -Dm644 "${pkgname%-git}/resources/com.gitlab.librebob.Athenaeum.desktop" \
		"$pkgdir/usr/share/applications/com.gitlab.librebob.Athenaeum.desktop"
	install -Dm644 "${pkgname%-git}/resources/com.gitlab.librebob.Athenaeum.appdata.xml" \
    	"$pkgdir/usr/share/appdata/com.gitlab.librebob.Athenaeum.appdata.xml"
}
