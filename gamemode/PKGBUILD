pkgname=gamemode
pkgver=1.5
pkgrel=1
pkgdesc="Optimise Linux system performance on demand"
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause')
depends=('polkit')
optdepends=('systemd')
makedepends=('meson' 'pkg-config' 'git')
install="$pkgname.install"
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('262389e0a625670c5f53f89d0c30a28cd5f9569a8e6b012dccf78b7fdcb98408'
            'SKIP')
validpgpkeys=('5AB43EC644CE6A84C39289F5048EF7331E7B6424')
              # Alex Smith <asmith@feralinteractive.com>

build() {
	cd "$pkgname-$pkgver"
	meson build \
		--prefix /usr \
		--libexecdir lib/gamemode \
		-Dwith-systemd-user-unit-dir=/usr/lib/systemd/user \
		-Dwith-pam-group=gamemode
	ninja -C build
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install

	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
