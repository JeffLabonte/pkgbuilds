pkgname=olivia-git
pkgver=r182.517136a
pkgrel=1
pkgdesc="Elegant music player"
arch=('x86_64')
url="https://github.com/keshavbhatt/olivia"
license=('MIT')
depends=('qt5-webkit' 'qt5-x11extras' 'sqlite' 'mpv' 'socat' 'python' 'wget')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/keshavbhatt/olivia')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	qmake
	make
	sed -i 's%Icon=${SNAP}/meta/gui/olivia.png%Icon=olivia.png%g' ${pkgname%-git}.desktop
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make INSTALL_ROOT="$pkgdir/" install
	install -Dm644 snap/gui/${pkgname%-git}.png $pkgdir/usr/share/pixmaps/${pkgname%-git}.png
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/${pkgname%-git}/LICENSE
}
