pkgname=ryzen-controller
pkgver=2.2.0
pkgrel=1
pkgdesc="A minimal Electron application to use ryzenAdj through a friendly interface"
arch=('x86_64')
url="https://gitlab.com/le.storm1er/ryzen-controller"
license=('CC0 1.0 Universal')
depends=('ryzenadj' 'nss' 'nodejs' 'gtk3' 'libxss')
makedepends=('npm')
conflicts=('ryzencontroller')
source=("https://gitlab.com/le.storm1er/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('7f445809e84aafa3f28eab7567d7d6ddfbf505b7529adfc2eb6960998968955a'
            'e1c7d2d4cfa10da7bc284c0fe4b94f5592ea2b5ab92854d74f23b70a98d8db7c')

build() {
	cd "$pkgname-$pkgver"
	npm install --cache "$srcdir/npm-cache"
	npm run-script package-linux
}

package() {
	cd "$pkgname-$pkgver"
	install -d "$pkgdir/"{opt/"$pkgname",usr/bin}
	cp -a release-builds/ryzencontroller-linux-x64/. "$pkgdir/opt/$pkgname"

	ln -s "/opt/$pkgname/ryzencontroller" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
}
