pkgname=ryzen-controller
pkgver=2.2.7
pkgrel=1
pkgdesc="Unlock the true performance of your Ryzen Mobile Processor"
arch=('x86_64')
url="https://www.ryzencontroller.com"
license=('CC0-1.0')
depends=('ryzenadj' 'nss' 'gtk3' 'libxss')
makedepends=('yarn' 'node-gyp' 'python2')
conflicts=('ryzencontroller')
source=("https://gitlab.com/ryzen-controller-team/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop"
        "$pkgname.png")
sha256sums=('be79d52ac928e3a6f39f2d2e85377117b5373746d14e7a10510c50e12c5020b0'
            'ecd3e87675cf35eb102bc5a0166402d4b25d0941a4f64f04bcfb93fd239fb054'
            '2a2d035008beb7c623e208bf5b6bf06c270157838c40d7375aefa9aee8f5f77f')

build() {
	cd "$pkgname-$pkgver"
	yarn install --cache "$srcdir/yarn-cache"
	yarn run dist-unpack
	yarn run dist-unpack:build
	yarn run dist-unpack:electron
}

package() {
	cd "$pkgname-$pkgver"
	rm -rf dist/linux-unpacked/resources/app.asar.unpacked/build

	install -d "$pkgdir/opt/$pkgname"
	cp -r dist/linux-unpacked/. "$pkgdir/opt/$pkgname"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/$pkgname/$pkgname" -t "$pkgdir/usr/bin"

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "$srcdir/$pkgname.png" -t "$pkgdir/usr/share/pixmaps"
}
