# https://aur.archlinux.org/packages/sparrow-wifi-git
pkgname=sparrow-wifi-git
pkgver=r147.a97885f
pkgrel=1
pkgdesc="Next-Gen GUI-based WiFi and Bluetooth Analyzer for Linux"
arch=('any')
url="https://github.com/ghostop14/sparrow-wifi"
license=('GPL3')
depends=('tk'
         'python-matplotlib'
         'python-qscintilla-qt5'
         'python-pyqt5'
         'qt5-declarative'
         'python-gps3'
         'python-manuf'
         'python-dateutil'
         'python-numpy'
         'python-sip'
         'python-pyqtchart')
makedepends=('git')
optdepends=('gpsd: GPS support'
            'bluez-hcitool: Bluetooth support'
            'bluez-hciconfig: Bluetooth support'
            'ubertooth-git: Ubertooth support'
            'python-dronekit: Drone communication support'
            'net-tools: for rpi.monitor_3dr script'
            'python-elasticsearch'
            'python-pytz')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=("opt/${pkgname%-git}/sparrowwifiagent.cfg")
install="${pkgname%-git}.install"
source=('git+https://github.com/ghostop14/sparrow-wifi.git'
        '37.patch'
        "${pkgname%-git}.desktop"
        "${pkgname%-git}-launcher")
sha256sums=('SKIP'
            'bb8a29014ac5f5b57f88f47e4d8de7ab2305cc8ddee45b0e3d0d7ee511dd0869'
            'dc37448598fae71601e3707f4be569d4858b155ecdc0ccc3927f3e5b97cce8fd'
            'dbe7e4bf50abb15d8685a32fdb74971a4b2f47353830dbf5236bd674f2cec5a3')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"

	# https://github.com/ghostop14/sparrow-wifi/pull/37
	git apply -3 "$srcdir/37.patch"

	sed -i 's|service gpsd status|systemctl status gpsd|g' scripts/*.sh
	sed -i 's|service gpsd stop|systemctl stop gpsd|g' scripts/*.sh
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -Dm644 sparrowwifiagent.cfg.sample \
		"$pkgdir/opt/${pkgname%-git}/sparrowwifiagent.cfg"
	cp -r *.py scripts "$pkgdir/opt/${pkgname%-git}"

	install -Dm644 docs/* -t "${pkgdir}/usr/share/doc/${pkgname%-git}"
	install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname%-git}"
	install -Dm644 wifi_icon.png "${pkgdir}/usr/share/pixmaps/${pkgname%-git}.png"

	install -Dm755 "$srcdir/${pkgname%-git}-launcher" -t "$pkgdir/usr/bin"
	install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t \
		"${pkgdir}/usr/share/applications"
}
