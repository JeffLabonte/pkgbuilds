# https://aur.archlinux.org/packages/git-cola
pkgname=git-cola
pkgver=3.8
pkgrel=1
pkgdesc="The highly caffeinated Git GUI"
arch=('any')
url="https://git-cola.github.io"
license=('GPL2')
depends=('git' 'python-pyqt5' 'qt5-svg')
#makedepends=('asciidoc' 'docbook-xsl' 'rsync' 'xmlto' 'python-sphinx')
makedepends=('python-sphinx')
optdepends=('python-pygments: syntax highlighting'
            'python-pyinotify: for inotify support'
            'tcl: to use the built-in ssh-askpass handler')
source=("git+https://github.com/git-cola/git-cola#tag=v$pkgver?signed")
sha256sums=('SKIP')
validpgpkeys=('FA41BF59C1B48E8C5F3DA61C8CE26BF4A9F606B0') # David Aguilar <davvid@gmail.com>

build() {
	cd "$srcdir/$pkgname"
#	make all doc html man
	make all man
}

package() {
	cd "$srcdir/$pkgname"
	export PYTHONHASHSEED=0
#	make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html,-man}
	make prefix=/usr DESTDIR="$pkgdir" install{,-man}
}
