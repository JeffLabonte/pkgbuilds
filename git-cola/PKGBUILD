# https://aur.archlinux.org/packages/git-cola
pkgname=git-cola
pkgver=3.9
pkgrel=1
pkgdesc="The highly caffeinated Git GUI"
arch=('any')
url="https://git-cola.github.io"
license=('GPL2')
depends=('git' 'python-pyqt5' 'qt5-svg')
makedepends=('asciidoc' 'docbook-xsl' 'rsync' 'xmlto' 'python-sphinx')
optdepends=('python-pygments: syntax highlighting'
            'python-pyinotify: for inotify support'
            'python-send2trash: enables "Send to Trash" functionality.'
            'tcl: to use the built-in ssh-askpass handler')
checkdepends=('python-pytest' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/git-cola/git-cola/archive/v$pkgver.tar.gz")
#source=("git+https://github.com/git-cola/git-cola.git#tag=v$pkgver?signed")
sha256sums=('66b62f479ad141322351d62aff76e4882719d08f8f29b35f97d30f355391a54e')
#validpgpkeys=('FA41BF59C1B48E8C5F3DA61C8CE26BF4A9F606B0') # David Aguilar <davvid@gmail.com>

build() {
	cd "$pkgname-$pkgver"
	export USE_ENV_PYTHON=1
	export GIT_COLA_NO_PRIVATE_LIBS=1
	export GIT_COLA_NO_VENDOR_LIBS=1
	make prefix=/usr all doc html man
}

check() {
	cd "$pkgname-$pkgver"

	# Run the unit tests
	make test
}

package() {
	cd "$pkgname-$pkgver"
	export USE_ENV_PYTHON=1
	export GIT_COLA_NO_PRIVATE_LIBS=1
	export GIT_COLA_NO_VENDOR_LIBS=1
	export PYTHONHASHSEED=0
	make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html,-man}
}
