# https://archlinux.org/packages/community/any/python-dropbox/
pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.2.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests python-stone)
makedepends=(python-setuptools python-wheel python-pip)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('81e06df2ef00b5441e40c93aba27b2cf37bbe676becdbbd44195094bffedf1e8cf3af68ef6d618eb98eb0ba4d335a7ee94a90390c32cb31a4aa971f92e4a9f4f')

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
