# https://archlinux.org/packages/community/any/python-dropbox/
pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.0.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests python-stone)
makedepends=(python-setuptools)
#checkdepends=(python-pytest-runner)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('35fea90964b006ff6561d2dda35afd9a28a6d61af149c125420fff4ab49ab9a0c2c541fdb7e948b40506a1a61c0937b5fcf60f0d8e14d5be41f3ed79ca3b409d')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd "$srcdir/$_pkgname-$pkgver"
#
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#  python2 setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
