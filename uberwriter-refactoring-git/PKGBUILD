pkgname=uberwriter-refactoring-git
_pkgname=uberwriter
pkgver=2.1.2.r20.gca75862
pkgrel=1
pkgdesc='A distraction free Markdown editor for GNU/Linux made with GTK+'
arch=('any')
url='http://uberwriter.github.io/uberwriter/'
license=('GPL3')
depends=('gtk3' 'pandoc' 'python-gtkspellcheck')
makedepends=('python-setuptools')
optdepends=('texlive-core' 'otf-fira-mono: Recommended font')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")
source=('git+https://github.com/UberWriter/uberwriter.git#branch=refactoring')
sha256sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd $_pkgname
    python setup.py build
}

package() {
    cd $_pkgname
    python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
