pkgname=abricotine-git
_pkgname=Abricotine
pkgver=0.7.0.r13.63de38d
pkgrel=1
pkgdesc="A markdown editor with inline preview"
arch=('x86_64')
url="http://abricotine.brrd.fr"
license=('GPL3')
depends=('gtk3' 'libnotify' 'nss' 'libxss' 'libxtst' 'xdg-utils' 'trash-cli' 'gvfs' 
         'pulseaudio' 'alsa-lib') #'kde-cli-tools' 'kdebase-runtime'
makedepends=('npm')
optdepends=('gnome-keyring'
            'lsb-release'
            'pandoc: for export funcion')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/brrd/${pkgname%-git}.git"
        "${pkgname%-git}.desktop")
sha256sums=('SKIP'
            'de31f395f39a50b7210692cddffd43ea3bf944fa038a315bebbfe3e3a79034de')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	npm install --cache "$srcdir/npm-cache"
	npm run packager
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/opt/${pkgname%-git}"
	cp -r dist/*/* "$pkgdir/opt/${pkgname%-git}"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/${pkgname%-git}/$_pkgname" "$pkgdir/usr/bin/${pkgname%-git}"

	install -Dm644 "../${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "icons/${pkgname%-git}@2x.png" "$pkgdir/usr/share/pixmaps/${pkgname%-git}.png"
}
