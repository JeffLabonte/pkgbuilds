pkgname=ooniprobe-desktop-git
pkgver=3.0.0.rc.6.r1.gebc193f
pkgrel=1
pkgdesc="The next generation OONI Probe desktop app"
arch=('x86_64')
url="https://ooni.org"
license=('MIT')
depends=('libnotify' 'libappindicator-gtk3' 'libxtst' 'nss' 'libxss')
makedepends=('git' 'yarn')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/ooni/probe-desktop.git"
        "${pkgname%-git}.desktop")
sha256sums=('SKIP'
            'baaf4f3cca079dddc0b4e048c8778c6cc84786bb88fd9d218424b7b9f04f1135')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$srcdir/${pkgname%-git}"

	# Don't download darwin & windows files
	#sed -i 's/(darwin|linux|windows)/linux/g' scripts/download-bin.js

	# Disable building of rpm & tar.gz
	sed -i 's/"deb",/"deb"/g' package.json
	sed -i '41,42d' package.json
}

build() {
	cd "$srcdir/${pkgname%-git}"
	yarn install --cache-folder "$srcdir/yarn-cache"
	yarn run probe-cli
	yarn run pack:linux
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -dm755 "$pkgdir/opt/OONI Probe"
	cp -a dist/linux-unpacked/* "$pkgdir/opt/OONI Probe"

	install -dm755 "$pkgdir/usr/bin"
	ln -sf "/opt/OONI Probe/${pkgname%-git}" "$pkgdir/usr/bin/${pkgname%-git}"

	install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications"

	for icon_size in 16 48; do
		icons_dir=/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
		install -d $pkgdir/$icons_dir
		install -m644 dist/.icon-set/icon_${icon_size}x${icon_size}.png \
			$pkgdir$icons_dir/${pkgname%-git}.png
	done

	for icon_size in 32 64 128 256 512 1024; do
		icons_dir=/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
		install -d $pkgdir/$icons_dir
		install -m644 dist/.icon-set/app_${icon_size}.png \
			$pkgdir$icons_dir/${pkgname%-git}.png
	done
}
