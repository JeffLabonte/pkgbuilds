pkgname=pacman-backup-files-git
pkgver=r9.1009b03
pkgrel=1
pkgdesc="Show pacnew and pacsave files"
arch=('any')
url="https://github.com/vetzki/pacman_backup_files"
license=('GPL3')
depends=('python' 'tk')
makedepends=('git' 'cython')
optdepends=('kate: default editor'
            'xterm: default terminal'
            'ttf-dejavu: default font')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://github.com/vetzki/pacman_backup_files.git")
sha256sums=('SKIP') # integrity checked in prepare

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/${pkgname%-git}"

	# create c file for main
	sha256sum -c integrity

	cython py_pbf/py_pbf_main.py --embed
}

build() {
	cd "$srcdir/${pkgname%-git}"

	_pyvers="$(python -V|awk '{print $2}'|cut -d "." -f 1-2)"

	python setup.py build_ext --inplace

	# maybe better add main also to setup.py
	gcc -O2 -I/usr/include/python${_pyvers}m -o py_pbf_main py_pbf/py_pbf_main.c \
		-lpython${_pyvers}m -lpthread -lm -lutil -ldl
}

package() {
	cd "$srcdir/${pkgname%-git}"

	_libdir="$pkgdir/usr/lib/${pkgname%-git}"

	# src
	find . -type f -name "*.so" -exec install -D "{}" $_libdir/"{}" \;
	install -D py_pbf_main $_libdir/py_pbf_main
	install -D py_pbf/py_pbf_defconf.py $_libdir/py_pbf_defconf.py

	# resources
	# TODO: adjust path in .desktop and .sh file
	install -Dm755 "resources/${pkgname%-git}.sh" "$pkgdir/usr/bin/${pkgname%-git}"

	# .desktop and icons
	install -Dm644 "resources/${pkgname%-git}.desktop" -t "$pkgdir"/usr/share/applications
	install -Dm644 resources/pacman-icon-symbolic.svg \
		"$pkgdir/usr/share/icons/${pkgname%-git}/pacman-icon-symbolic.svg"
	install -Dm644 resources/pacman-icon.png -t \
		"$pkgdir/usr/share/icons/${pkgname%-git}/pacman-icon.png"
}
