pkgname=touchpad-indicator
pkgver=2.1.3
pkgrel=1
_priority=extras
_ubunturel=18.04.0
pkgdesc="An indicator for the touchpad"
arch=('any')
url="https://www.atareao.es"
license=('GPL3')
depends=('gtk3' 'gdk-pixbuf2' 'libappindicator-gtk3' 'libnotify' 'gconf' 'librsvg' 'python-xlib' 'python-pyudev' 'python-dbus' 'xorg-xinput')
optdepends=()
conflicts=("$pkgname-bzr")
source=("https://launchpad.net/~atareao/+archive/ubuntu/atareao/+sourcefiles/$pkgname/${pkgver}-0${_priority}${_ubunturel}/${pkgname}_${pkgver}-0${_priority}${_ubunturel}.tar.xz")
md5sums=('7a838dea7228de4d51925671e423da74')

package() {
	cd "$pkgname"
	install -Dm755 bin/$pkgname $pkgdir/usr/bin/$pkgname
	install -Dm644 debian/changelog $pkgdir/usr/share/$pkgname/changelog
	cp -a src/. $pkgdir/usr/share/$pkgname
	install -d $pkgdir/usr/share/{$pkgname/icons,glib-2.0/schemas}
	cp -a data/icons/*.svg $pkgdir/usr/share/$pkgname/icons
	cp -a data/schemas/. $pkgdir/usr/share/glib-2.0/schemas
	install -Dm755 data/00_check_touchpad_status $pkgdir/etc/pm/sleep.d/00_check_touchpad_status
	install -Dm755 data/00_check_touchpad_status_systemd $pkgdir/usr/lib/systemd/system-sleep/00_check_touchpad_status_systemd
	install -Dm644 data/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
	install -Dm644 data/$pkgname-autostart.desktop $pkgdir/usr/share/$pkgname/$pkgname-autostart.desktop
	install -Dm644 data/icons/$pkgname.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg
	for icon_size in 8 16 32 48 96 128 192 256 512 1024; do
        icons_dir=/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps
        install -d $pkgdir/$icons_dir
        install -m644 data/icons/${icon_size}x${icon_size}/$pkgname.png $pkgdir$icons_dir/$pkgname.png
    done
}
