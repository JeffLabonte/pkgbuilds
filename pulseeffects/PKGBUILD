pkgname=pulseeffects
pkgver=4.4.2
pkgrel=1
pkgdesc='Audio Effects for Pulseaudio Applications'
arch=('x86_64')
url='https://github.com/wwmm/pulseeffects'
license=('GPL3')
depends=('gtk3' 'gtkmm3' 'glibmm' 'libpulse' 'gstreamer' 'gst-plugins-good' 'gst-plugins-bad'
        'lilv' 'boost-libs' 'libsigc++' 'libsndfile' 'libsamplerate' 'zita-convolver' 'libebur128')
makedepends=('meson' 'boost' 'itstool' 'appstream-glib'
             'calf' 'zam-plugins' 'rubberband' 'mda.lv2')
optdepends=('calf: limiter, compressor exciter, bass enhancer and others'
            'zam-plugins: maximizer'
            'rubberband: pitch shifting'
            'mda.lv2: loudness')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('886c57f9a503312d027d10aee59d346fb94177d1cb56a82f969b54a30e2791a57855faaec90365db2241f14c1200c7569e0ae23840df113e1c2dc675b2dc1820')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson ..

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
