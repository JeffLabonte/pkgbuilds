# Maintainer: Your Name <youremail@domain.com>
pkgname=ryzen-controller-git
pkgver=1.13.4.r7.f6e8d09
pkgrel=1
pkgdesc="A minimal Electron application to use ryzenAdj through a friendly interface"
arch=('x86_64')
url="https://gitlab.com/le.storm1er/ryzen-controller"
license=('CC0 1.0 Universal')
depends=('ryzenadj' 'nss' 'nodejs' 'gtk3' 'libxss')
makedepends=('git' 'npm')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://gitlab.com/le.storm1er/ryzen-controller.git'
	"${pkgname%-git}.desktop")
sha256sums=('SKIP'
            'e1c7d2d4cfa10da7bc284c0fe4b94f5592ea2b5ab92854d74f23b70a98d8db7c')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	npm install --cache "${srcdir}/npm-cache"
	npm audit fix
	npm run-script package-linux
}

package() {
	cd "$srcdir/${pkgname%-git}"
	install -d "$pkgdir/"{opt/"${pkgname%-git}",usr/bin}
	cp -a release-builds/ryzencontroller-linux-x64/. \
		"$pkgdir/opt/${pkgname%-git}"
	ln -s "/opt/${pkgname%-git}/ryzencontroller" "$pkgdir/usr/bin/${pkgname%-git}"
	install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
