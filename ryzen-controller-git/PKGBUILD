pkgname=ryzen-controller-git
pkgver=2.2.7.r0.b725856
pkgrel=1
pkgdesc="Unlock the true performance of your Ryzen Mobile Processor"
arch=('x86_64')
url="https://www.ryzencontroller.com"
license=('CC0-1.0')
depends=('ryzenadj' 'nss' 'gtk3' 'libxss')
makedepends=('git' 'yarn' 'node-gyp' 'python2')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" 'ryzencontroller')
source=('git+https://gitlab.com/ryzen-controller-team/ryzen-controller.git'
        "${pkgname%-git}.desktop"
        "${pkgname%-git}.png")
sha256sums=('SKIP'
            'ecd3e87675cf35eb102bc5a0166402d4b25d0941a4f64f04bcfb93fd239fb054'
            '2a2d035008beb7c623e208bf5b6bf06c270157838c40d7375aefa9aee8f5f77f')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	yarn install --cache "$srcdir/yarn-cache"
	yarn run dist-unpack
	yarn run dist-unpack:build
	yarn run dist-unpack:electron
}

package() {
	cd "$srcdir/${pkgname%-git}"
	rm -rf dist/linux-unpacked/resources/app.asar.unpacked/build

	install -d "$pkgdir/opt/${pkgname%-git}"
	cp -r dist/linux-unpacked/. "$pkgdir/opt/${pkgname%-git}"

	install -d "$pkgdir/usr/bin"
	ln -s "/opt/${pkgname%-git}/${pkgname%-git}" -t "$pkgdir/usr/bin"

	install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/${pkgname%-git}"
	install -Dm644 "$srcdir/${pkgname%-git}.desktop" -t "$pkgdir/usr/share/applications"
	install -Dm644 "$srcdir/${pkgname%-git}.png" -t "$pkgdir/usr/share/pixmaps"
}
