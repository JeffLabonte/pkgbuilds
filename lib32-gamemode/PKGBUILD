pkgname=lib32-gamemode
_pkgname=${pkgname#lib32-}
pkgver=1.5.1
pkgrel=1
pkgdesc="A daemon/lib combo for Linux that allows games to request a set of optimisations be temporarily applied to the host OS."
arch=('x86_64')
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause')
depends=('polkit' 'gamemode' 'lib32-glibc' 'lib32-systemd' 'lib32-dbus')
makedepends=('git' 'meson')
source=("git+https://github.com/FeralInteractive/gamemode.git#tag=$pkgver"
        'git+https://github.com/benhoyt/inih.git')
sha256sums=('SKIP'
            'SKIP')

build() {
	export CFLAGS=-m32
	export CXXFLAGS=-m32
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	meson $_pkgname build  \
		--prefix /usr \
		--libdir lib32 \
		-Dwith-daemon=false \
		-Dwith-examples=false \
		-Dwith-systemd=false \
		-Dwith-util=false
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install

	rm -rf "$pkgdir/usr/include"

	cd "$srcdir/$_pkgname"
	install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
