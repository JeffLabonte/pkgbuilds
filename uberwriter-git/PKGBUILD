pkgname=uberwriter-git
pkgver=2.2.0.beta1.4.r117.gd75a14d
pkgrel=1
pkgdesc="A distraction free Markdown editor for GNU/Linux made with GTK+"
arch=('any')
url="https://uberwriter.github.io"
license=('GPL3')
depends=('gtk3' 'webkit2gtk' 'gspell' 'python-pypandoc' 'python-regex' 'python-levenshtein' 
	'python-pyenchant' 'python-cairo')
makedepends=('python-setuptools')
optdepends=('texlive-core: for the pdftex module' 'otf-fira-mono: Recommended font')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/UberWriter/uberwriter.git')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/${pkgname%-git}"
    python setup.py build
}

package() {
    cd "$srcdir/${pkgname%-git}"
    python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
