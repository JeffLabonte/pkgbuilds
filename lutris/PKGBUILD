# https://www.archlinux.org/packages/community/any/lutris
groups=('modified')

pkgname=lutris
pkgver=0.5.8.2
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'python-dbus' 'gtk3'
         'glib2' 'psmisc' 'cabextract' 'unzip' 'p7zip' 'curl' 'xorg-xrandr'
         'python-pillow' 'python-requests' 'gnome-desktop' 'webkit2gtk'
         'mesa-demos' 'python-dbus' 'python-distro' 'python-magic-ahupp'
         'python-lxml')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'python-nose-cover3' 'wine' 'xterm')
optdepends=('wine: Windows support'
            'vulkan-icd-loader: Vulkan support'
            'lib32-vulkan-icd-loader: Vulkan support'
            'vkd3d: Vulkan 3D support'
            'lib32-vkd3d: Vulkan 3D support'
            'gvfs: GVFS backend'
            'gamemode: Request a set of optimisations be temporarily applied to host OS'
            "mangohud: Display the games' FPS + other information")
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lutris/lutris/archive/v$pkgver.tar.gz")
sha512sums=('20fa4582bbd57c71a305a64529d9d169c43c168507f3ad275a41165cb3c117850ee5160e9c7a3f78d091ff0250f8c3ea17b2b4ea00d19863c6ba0add2154d1dd')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
