# https://aur.archlinux.org/packages/authy-snap
pkgname=authy
pkgver=1.8.1
pkgrel=1
pkgdesc="Two factor authentication desktop application"
arch=('x86_64')
url="https://authy.com"
license=('unknown')
depends=('nss' 'gtk3' 'libxss' 'libdbusmenu-glib')
makedepends=('squashfs-tools')
conflicts=("$pkgname-snap")
replaces=("$pkgname-snap")
_snapid="H8ZpNgIoPyvmkgxOWw5MSzsXK1wRZiHn"
_snaprev="3"
source=("$pkgname-$pkgver-$_snaprev.snap::https://api.snapcraft.io/api/v1/snaps/download/${_snapid}_${_snaprev}.snap")
sha256sums=('b1466c9b0d5a568ae29d1abaf220c4adaa05ea2d95825ebefdd714a7ce566380')

prepare() {
    unsquashfs -f -d "$srcdir/$pkgname" "$pkgname-$pkgver-$_snaprev.snap"

	cd "$srcdir/$pkgname"
    sed -i 's|${SNAP}/meta/gui/icon.png|authy|g' "meta/gui/$pkgname.desktop"
}

package() {
	cd "$srcdir/$pkgname"
    install -d "$pkgdir/opt/$pkgname"
    cp -r * "$pkgdir/opt/$pkgname"
    install -Dm644 "meta/gui/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
    install -Dm644 meta/gui/icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
    install -d "$pkgdir/usr/bin"
    ln -s "/opt/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
