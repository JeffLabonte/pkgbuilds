# https://aur.archlinux.org/packages/authy-snap
pkgname=authy
pkgver=1.8.2
pkgrel=1
pkgdesc="Two factor authentication desktop application"
arch=('x86_64')
url="https://authy.com"
license=('unknown')
depends=('nss' 'gtk3' 'libxss')
makedepends=('squashfs-tools')
conflicts=("$pkgname-snap")
replaces=("$pkgname-snap")
_snapid='H8ZpNgIoPyvmkgxOWw5MSzsXK1wRZiHn'
_snaprev='4'
source=("$pkgname-$pkgver-$_snaprev.snap::https://api.snapcraft.io/api/v1/snaps/download/${_snapid}_${_snaprev}.snap")
sha256sums=('2e01b7d8f913eec54d992e9144d679840948d484f34bc7b444f20f3bf1cdc859')

prepare() {
    unsquashfs -f -d "$srcdir/$pkgname" "$pkgname-$pkgver-$_snaprev.snap"

	cd "$srcdir/$pkgname"
    sed -i 's|${SNAP}/meta/gui/icon.png|authy|g' "meta/gui/$pkgname.desktop"
}

package() {
	cd "$srcdir/$pkgname"
    install -d "$pkgdir/opt/$pkgname"
    cp -r * "$pkgdir/opt/$pkgname"
    rm -rf "$pkgdir/opt/$pkgname"/{data-dir,gnome-platform,lib,meta,scripts,usr,*.sh}

    install -Dm644 "meta/gui/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
    install -Dm644 meta/gui/icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

    install -d "$pkgdir/usr/bin"
    ln -s "/opt/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
